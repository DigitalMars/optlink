		TITLE	CMDLINE - Copyright (c) SLR Systems 1994

		INCLUDE	MACROS
		INCLUDE	IO_STRUC
		INCLUDE	MODULES
		INCLUDE	EXES

		PUBLIC	PROCESS_POSIT_CMDLINE,SET_TINY,SET_BINARY,GETNST_SLASH,GET_NUMBER16,SET_STACK_1,GET_NUMBER

		;basic command line functions

		EXTERNDEF	TFLAGS:BYTE


		.DATA

		EXTERNDEF	OUTBUF:BYTE,SYMBOL_TEXT:BYTE,RELOAD_BYTE:BYTE,MODEL_FAR_BYTE:BYTE,DEBUG_BYTE:BYTE,CACHE_BYTE:BYTE
		EXTERNDEF	COMPRESS_BYTE:BYTE,CV_INFO_TYPE:BYTE,CODEVIEW_BYTE:BYTE,LAST_DELIM:BYTE,TD_INFO_TYPE:BYTE
		EXTERNDEF	DEBUG_TYPES_SELECTED:BYTE,EXETYPE_FLAG:BYTE,EXETYPE_CMDLINE_FLAG:BYTE

		EXTERNDEF	FILESTUFF_PTR:DWORD,OVERLAY_INTERRUPT_NUMBER:DWORD,RELOAD_DEPTH:DWORD,INPTR1:DWORD,CACHE_ORDER:DWORD
		EXTERNDEF	PLINK_LEVEL:DWORD,DONT_PACK:DWORD,PACKCODE:DWORD,PACKDATA:DWORD,PADCODE:DWORD,PADDATA:DWORD
		EXTERNDEF	SYMBOL_LENGTH:DWORD,FLAG_0C:DWORD,_OUTFILE_GINDEX:DWORD,MAPFILE_GINDEX:DWORD,TOKSTR:DWORD
		EXTERNDEF	CURN_SECTION_GINDEX:DWORD,PACK_DEFAULT_SIZE:DWORD,TLINK_OUTTYPE:DWORD,IMPLIB_PAGESIZE:DWORD
		EXTERNDEF	IMPLIB_FILE_GINDEX:DWORD,IMPLIB_COFF_FILE_GINDEX:DWORD,IMPDEF_FILE_GINDEX:DWORD
		EXTERNDEF	CACHE_REG_TABLE:DWORD,CACHE_UMB_TABLE:DWORD
		EXTERNDEF	CACHE_EMS_TABLE:DWORD,CACHE_XMS_TABLE:DWORD,CURN_OUTFILE:DWORD,SYS_BASE:DWORD,DEFFILE:DWORD
		EXTERNDEF	STACK_SIZE:DWORD,PE_BASE:DWORD,HEAP_SIZE:DWORD,ALIGNMENT:DWORD,ENTRYPOINT_TXT:DWORD

		EXTERNDEF	EXEHEADER:EXE,NEXEHEADER:NEXE,PEXEHEADER:PEXE,NULNAM:NFN_STRUCT,FILNAM:NFN_STRUCT,SRCNAM:NFN_STRUCT
		EXTERNDEF	SYMBOL_TPTR:TPTR_STRUCT


		.CODE	FILEPARSE_TEXT

		EXTERNDEF	_err_abort:proc,MOVE_FILNAM:PROC,SET_DEFAULTS:PROC,RES_LOGO_OUTPUT:PROC
		EXTERNDEF	RES_ECHO_IND:PROC,NOIGNORECASE_PROC:PROC,HANDLE_RCS:PROC,MOVE_SOURCE_TO_FILNAM:PROC
		EXTERNDEF	ERR_DSSI_SLASH_ABORT:PROC,SET_ECHO_IND:PROC,IGNORECASE_PROC:PROC,_abort:proc,_check_nul:proc
		EXTERNDEF	NOCHECKSUM_PROC:PROC,RET_FALSE:PROC,LOUTALL_CON:PROC,_do_objs:proc
		EXTERNDEF	YYLEX_FILENAME:PROC,PROCESS_FREE_CMDLINE:PROC,SCAN_HELPER:PROC,GETNST:PROC,GET_KEYWORD:PROC
		EXTERNDEF	ERR_SYMBOL_TEXT_ABORT:PROC,VERBOSE_PROC:PROC,ERR_INBUF_ABORT:PROC,HELP_PROC_1:PROC
		EXTERNDEF	SET_REORDER_ALLOWED:PROC,SILENT_PROC:PROC,RES_REORDER_ALLOWED:PROC,_filename_install:proc
		EXTERNDEF	INSTALL_OUTFILE:PROC,STORE_FILNAM:PROC,HANDLE_LIBS:PROC,_do_srcnam:proc,_do_search_library:proc
		EXTERNDEF	DO_NEW_SECTION:PROC,VERIFY_SECTION:PROC,ENDAREA_PROC:PROC,REOPEN_AREA:PROC,LOWERCASE_PROC:PROC
		EXTERNDEF	UPPERCASE_PROC:PROC,CHECKSUM_PROC:PROC,OVLCODEFIRST_PROC:PROC,OVLCODELAST_PROC:PROC
		EXTERNDEF	ERR_SYMBOL_TEXT_RET:PROC,NONULLSDOSSEG_PROC:PROC,NOGROUPSTACK_PROC:PROC,GROUPSTACK_PROC:PROC
		EXTERNDEF	ERRORDELETE_PROC:PROC,NOERRORDELETE_PROC:PROC,ERR_INBUF_RET:PROC,WARN_SYMBOL_TEXT_RET:PROC
		EXTERNDEF	YYLEX_SYMBOL:PROC,FORCE_INSTALL_MODULE:PROC,DOSSEG_PROC:PROC,RC_PROTMODE:PROC,HANDLE_DEFS:PROC
		EXTERNDEF	RES_PACKFUNCTIONS:PROC,SET_PACKFUNCTIONS:PROC,SEE_NEXT:PROC,MOVE_DEFAULT_FLAGS:PROC
		EXTERNDEF	SET_CASE_MODE:PROC,GET_NEXT:PROC,FORCE_SIGNON:PROC,HANDLE_STUB:PROC,GET_NUM_DOT_NUM:PROC
		EXTERNDEF	GET_SYMBOL:PROC,SET_SUBSYSTEM:PROC,SET_SUBSYS_VERSION:PROC

		EXTERNDEF	RCSTUFF:CMDLINE_STRUCT,IMPLIB_STUFF:CMDLINE_STRUCT,IMPDEF_STUFF:CMDLINE_STRUCT
		EXTERNDEF	STUBSTUFF:CMDLINE_STRUCT,RESSTUFF:CMDLINE_STRUCT,OBJSTUFF:CMDLINE_STRUCT,LIBSTUFF:CMDLINE_STRUCT
		EXTERNDEF	EXESTUFF:CMDLINE_STRUCT,MAPSTUFF:CMDLINE_STRUCT,DEFSTUFF:CMDLINE_STRUCT,OVLSTUFF:CMDLINE_STRUCT
		EXTERNDEF	PATHSTUFF:CMDLINE_STRUCT

		EXTERNDEF	CMDLIN_ERR:ABS,MISSING_COLON_ERR:ABS,ILL_NUMBER_ERR:ABS,DUP_CACHE_ERR:ABS,CACHE_VALUE_ERR:ABS
		EXTERNDEF	SECTION_REQ_ERR:ABS,PAREN_NESTING_ERR:ABS,MULTIPLE_RC_ERR:ABS,TLINK_XTRA_ERR:ABS
		EXTERNDEF	CANT_WINPACK_ERR:ABS,DUP_STUB_ERR:ABS


PROCESS_POSIT_CMDLINE	PROC
		;
		;PROCESS A POSITIONAL MS-STYLE CMDLINE
		;
		;NOW, START PARSING THE COMMAND INPUT...
		;
		CALL	PARSE_OBJS
if	fg_plink
		JC	L99$			;COMMAND WAS /FREEFORMAT
endif
		CALL	PARSE_OUTFILE
		CALL	PARSE_MAPFILE
		CALL	PARSE_LIBS
if	fg_segm OR fg_pe
		CALL	PARSE_DEFFILE
		CALL	PARSE_RCS
endif
		RET

if	fg_plink
L99$:
		JMP	PROCESS_FREE_CMDLINE
endif

PROCESS_POSIT_CMDLINE	ENDP


PARSE_OBJS	PROC	NEAR
		;
		;HERE COME OBJS TO LINK
		;
		MOV	FILESTUFF_PTR,OFF OBJSTUFF
		CALL	VERIFY_SECTION
L1$:
		CALL	GETNST_SLASH		;HANDLE ANY SLASH COMMANDS...
		JC	L99$			;COMMAND WAS /FREEFORMAT...
		CMP	AL,','
		JZ	L65$			;OK, GO READ .EXE FILE
		CMP	AL,0DH
		JZ	L4$
		MOV	LAST_DELIM,0
		CMP	AL,'+'
		JZ	L3$			;ALLOWS IGNORING A CR
if	fg_ov
		CMP	AL,'('
		JZ	L5$
		CMP	AL,')'
		JZ	L6$
endif
		CMP	AL,';'
		JZ	L7$
		CMP	AL,1AH
		JZ	L7$
L2$:
		DEC	INPTR1
		RESS	PLUS_FOUND
		MOV	EAX,OFF OBJSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		push	EAX
		call	_do_objs
		add	ESP,4
		JMP	L1$

L99$:
		RESS	PLUS_FOUND
		STC
		RET

L65$:
		;
		;GOT A COMMA
		;IF NEXT NON-BLANK IS A CR, DON'T COUNT IT...
		;
		CMP	LAST_DELIM,0DH		;IF LAST DELIMITER WAS A 0DH, SKIP THIS
		JMP	L45$

L4$:
		;
		;FOUND CR
		;
		CMP	LAST_DELIM,','
L45$:
		MOV	LAST_DELIM,0
		JNZ	L46$
if	fg_td
		BITT	TLINK_DEFAULT_MODE
		JNZ	L46$
endif
		JMP	L1$

L46$:
		MOV	LAST_DELIM,AL
		CMP	SRCNAM.NFN_PRIMLEN,0	;IGNORE IF NO .OBJS YET
		JZ	L1$
		BITT	PLUS_FOUND
		PUSHFD
		RESS	PLUS_FOUND
		POPFD
		JZ	L71$
if	fg_td
		CMP	AL,0DH			;PLUS OVERIDES A CR ALWAYS
		JZ	L1$
		BITT	TLINK_DEFAULT_MODE	;IN TLINK MODE, THAT'S ALL IT OVERIDES
		JNZ	L71$
endif
		JMP	L1$

L3$:
		;
		;FOUND +
		;
		SETT	PLUS_FOUND
		JMP	L1$

if	fg_ov

L5$:
		;
		;FOUND (
		;
		BITT	PARSE_IN_OVERLAY	;IF ALREADY IN OVERLAY, ( IS FIRST CHAR OF FILENAME
		JNZ	L2$
		SETT	PARSE_IN_OVERLAY
		CALL	REOPEN_AREA
		CALL	DO_NEW_SECTION		;SETUP NEW SECTION
		OR	FNTBL+')',MASK FNTBL_ILLEGAL
L59$:
		RESS	PLUS_FOUND
		JMP	L1$

L6$:

		;
		; ) IS END OF AN OVERLAY SECTION, UNLESS WE AREN'T IN ONE...
		;
		BITT	PARSE_IN_OVERLAY
		JZ	L61$
		RESS	PARSE_IN_OVERLAY
		AND	FNTBL+')',NOT MASK FNTBL_ILLEGAL
		CALL	ENDAREA_PROC
		JMP	L59$

endif

L7$:
		DEC	INPTR1
L71$:
		RESS	PLUS_FOUND
if	fg_ov
		BITT	PARSE_IN_OVERLAY
		JNZ	L61$
endif
		RET

if	fg_ov
L61$:
		MOV	AL,PAREN_NESTING_ERR
		CALL	ERR_INBUF_ABORT

endif

PARSE_OBJS	ENDP


GETNST_SLASH	PROC
		;
		;
		;
L1$:
		CALL	GETNST
		CMP	AL,'/'
		JZ	L5$
		CMP	AL,'-'
		JZ	L5$
		CLC
		RET

if	fg_td
L3$:
		CALL	PARSE_TLINK_SWITCH
		SETT	SLASH_FOUND
		JMP	L1$
endif

L5$:
		;
		;PROCESS COMMANDS FROM COMMAND LINE...
		;THIS IS JUMP TABLE HANDLER.
		;
		;COMMTBL LOOKS LIKE:
		;COMMTBL	DB	3	;MAX LENGTH OF COMMAND
		;		DB	3	;SIGNIFICANT CHARACTERS
		;		DW	COPROC	;ROUTINE IF THIS COMMAND
		;		DB	'CO:'	;ASCII FOR COMMAND
		;
		;		ETC
		;		ETC
		;DS & ES ARE DGROUP
		;
		RESS	PLUS_FOUND
		MOV	LAST_DELIM,0
		CALL	GET_KEYWORD	;PARSE IT OUT TO UPPER CASE
		XOR	EAX,EAX
		MOV	AL,SYMBOL_TEXT	;FIRST CHAR OF TOKEN
if	fg_td
		BITT	TLINK_SYNTAX
		JNZ	L3$
endif
		SUB	AL,'A'		;DO TABLE LOOKUP ON START
		CMP	AL,'Z'-'A'
		JA	TRY_?

		MOV	ESI,MS_CMD_PTRS[EAX*4]	;START LOOKING HERE
MS_CHECK_NEXT:
		MOV	DL,[ESI]	;MAX LENGTH
		INC	ESI
		AND	EDX,03FH
		JZ	MS_BAD_CMD	;END OF TABLE
		MOV	BL,[ESI]
		INC	ESI		;SIG_CHARS
		MOV	ECX,EDX
		MOV	EAX,ESI
		ADD	ESI,4
		MOV	EDI,OFF SYMBOL_TEXT
		REPE	CMPSB
		JZ	MS_MATCHED
		ADD	ESI,ECX		;ADJUST SI FOR NEXT TOKEN
		CMP	BPTR [EDI-1],CH	;DID WE REACH END OF TOKEN?
		JNZ	MS_CHECK_NEXT	;NO, TRY NEXT
		INC	ECX
		SUB	DL,CL
		CMP	DL,BL		;IS IT AT LEAST MINMATCH LONG?
		JC	MS_CHECK_NEXT
MS_EXEC_THIS:
		CALL	DPTR [EAX]
		SETT	SLASH_FOUND
		JMP	L1$

MS_MATCHED:
		CMP	[EDI],CL
		JNZ	MS_CHECK_NEXT
		JMP	MS_EXEC_THIS

TRY_?:
		CMP	AL,'?'-'A'
		JNZ	MS_BAD_CMD
		JMP	MS_HELP

MS_BAD_CMD:
		;
		;SKIP TO END OF COMMAND...
		;
		MOV	AL,CMDLIN_ERR
		CALL	WARN_SYMBOL_TEXT_RET
		JMP	L1$

GETNST_SLASH	ENDP


PARSE_OUTFILE	PROC	NEAR
		;
		;GET NAME OF OUTPUT FILE (USE FIRST OBJ AS DEFAULT)
		;
		MOV	FILESTUFF_PTR,OFF EXESTUFF
L1$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L1$			;YEA, SKIP THOSE
		CMP	AL,','
		JZ	L6$			;GOT A COMMA
		CMP	AL,0DH
		JZ	L7$
L2$:
		DEC	INPTR1
L3$:
		MOV	EAX,OFF EXESTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM

		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;EAX IS GINDEX, ECX IS FILE_LIST PHYS
		ASSUME	ECX:PTR FILE_LIST_STRUCT

		OR	[ECX].FILE_LIST_FLAGS,MASK FLF_RANDOM
		CALL	INSTALL_OUTFILE
		ASSUME	ECX:NOTHING

		MOV	_OUTFILE_GINDEX,EAX

		MOV	EAX,OFF FILNAM
		push	EAX
		call	_check_nul
		add	ESP,4
		MOV	EAX,OFF FILNAM
		TEST	[EAX].NFN_STRUCT.NFN_FLAGS,MASK NFN_NUL
		JNZ	L5$
		push	EAX
		call	_do_srcnam
		add	ESP,4
L5$:
		;
		;(SE) Test for .DLL output (to avoid .def file)
		;
		MOV	EAX,OFF FILNAM
		ASSUME	EAX:PTR NFN_STRUCT

		CMP	[EAX].NFN_EXTLEN,4
		JNE	L51$

		MOV	ECX,[EAX].NFN_TOTAL_LENGTH

		LEA	ECX,[EAX+ECX].NFN_TEXT

		MOV	ECX,[ECX-4]

		OR	ECX,20202000h

		CMP	ECX,'lld.'
		JNE	L51$

		ASSUME	EAX:NOTHING
		OR	FLAG_0C,MASK APPTYPE		;OUTPUT A LIBRARY
L51$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L5$			;SKIP ANY TRAILING +'S
		CMP	AL,0DH
		JZ	L9$
		CMP	AL,','
		JNZ	L8$
		;
		;MAKE SURE DEFAULT USED FOR MAP FILE
		;
		SETT	MAPFILE_SELECTED
		JMP	L9$

L8$:
		DEC	INPTR1
L9$:
		MOV	LAST_DELIM,AL
		RET

L6$:
		;
		;GOT A COMMA, SKIP IT IF WE JUST HAD A CR
		;
		CMP	LAST_DELIM,0DH
		JZ	L3$
		JMP	L2$

L7$:
		;
		;GOT A CR, SKIP IT IF WE JUST HAD A COMMA
		;
		CMP	LAST_DELIM,','
		JZ	L3$
		JMP	L2$

PARSE_OUTFILE	ENDP


PARSE_MAPFILE	PROC	NEAR
		;
		;GET NAME OF MAP FILE (USE OUTFILE AS DEFAULT IF SELECTED)
		;
		MOV	FILESTUFF_PTR,OFF MAPSTUFF
L1$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L1$		;SKIP ANY LEADING +'S
		CMP	AL,','
		JZ	L6$
		CMP	AL,0DH
		JZ	L7$
L2$:
		DEC	INPTR1
L3$:
		MOV	EAX,OFF MAPSTUFF
		CALL	YYLEX_FILENAME
		SETT	SKIP_OUTFILE_LINK
		MOV	EAX,OFF FILNAM
		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;EAX IS GINDEX, ECX IS FILE_LIST PHYS

		CALL	INSTALL_OUTFILE		;THIS IS AN OUTPUT FILE

		MOV	MAPFILE_GINDEX,EAX
		XOR	ECX,ECX
		MOV	EAX,OFF FILNAM
		RESS	SKIP_OUTFILE_LINK,CL
		RESS	MAPFILE_SELECTED,CL
		CMP	[EAX].NFN_STRUCT.NFN_PRIMLEN,ECX
		JZ	L5$
		push	EAX
		call	_check_nul
		add	ESP,4
		MOV	EAX,OFF FILNAM
		TEST	[EAX].NFN_STRUCT.NFN_FLAGS,MASK NFN_NUL
		JNZ	L5$
		SETT	MAPFILE_SELECTED
		SETT	SEGMENTS_MAP
L5$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L5$			;SKIP ANY TRAILING +'S
		CMP	AL,0DH
		JZ	L9$
		CMP	AL,','
		JZ	L9$
L8$:
		DEC	INPTR1
L9$:
		MOV	LAST_DELIM,AL
		RET

L6$:
		;
		;GOT A COMMA, SKIP IT IF WE JUST HAD A CR
		;
		CMP	LAST_DELIM,0DH
		JZ	L3$
		JMP	L2$

L7$:
		;
		;GOT A CR, SKIP IT IF WE JUST HAD A COMMA
		;
		CMP	LAST_DELIM,','
		JZ	L3$
		JMP	L2$

PARSE_MAPFILE	ENDP


PARSE_DEFFILE	PROC	NEAR
		;
		;GET NAME OF MAP FILE (USE OUTFILE AS DEFAULT IF SELECTED)
		;
		MOV	FILESTUFF_PTR,OFF DEFSTUFF
L1$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L1$			;SKIP ANY LEADING +'S
if	fg_def
		CMP	AL,','
		JZ	L6$
		CMP	AL,0DH
		JZ	L7$
L2$:
		DEC	INPTR1
L3$:
		MOV	EAX,OFF DEFSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		CALL	STORE_FILNAM

		XOR	ECX,ECX
		MOV	DEFFILE,EAX
		MOV	EAX,OFF FILNAM
		RESS	DEFFILE_SELECTED,CL
		CMP	[EAX].NFN_STRUCT.NFN_PRIMLEN,ECX
		JZ	L5$
		push	EAX
		call	_check_nul
		add	ESP,4
		MOV	EAX,OFF FILNAM
		TEST	[EAX].NFN_STRUCT.NFN_FLAGS,MASK NFN_NUL
		JNZ	L5$
		SETT	DEFFILE_SELECTED
L5$:
		CALL	GETNST_SLASH
		CMP	AL,'+'
		JZ	L5$			;SKIP ANY TRAILING +'S
		CMP	AL,','
		JZ	L9$
		CMP	AL,0DH
		JZ	L9$
		DEC	INPTR1
L9$:
endif
		MOV	LAST_DELIM,AL
		RET

if	fg_def

L6$:
		;
		;GOT A COMMA, SKIP IT IF WE JUST HAD A CR
		;
		CMP	LAST_DELIM,0DH
		JZ	L3$
		JMP	L2$

L7$:
		;
		;GOT A CR, SKIP IT IF WE JUST HAD A COMMA
		;
		CMP	LAST_DELIM,','
		JZ	L3$
		JMP	L2$

endif

PARSE_DEFFILE	ENDP


PARSE_LIBS	PROC	NEAR
		;
		;GET NAMES OF LIBRARIES TO SEARCH
		;
		MOV	FILESTUFF_PTR,OFF LIBSTUFF
L1$:
		CALL	GETNST_SLASH
		CMP	AL,','
		JZ	L6$			;SKIP COMMA
		CMP	AL,0DH
		JZ	L4$			;TERMINATE IF PLUS NOT FOUND
		MOV	LAST_DELIM,0
		CMP	AL,'+'
		JZ	L3$			;FLAG + FOUND
		DEC	INPTR1
		CMP	AL,';'
		JZ	L9$
		CMP	AL,1AH
		JZ	L9$
		RESS	PLUS_FOUND
		MOV	EAX,OFF LIBSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		CALL	HANDLE_LIBS
		JMP	L1$

L3$:
		SETT	PLUS_FOUND
		JMP	L1$

L6$:
		;
		;COMMA FOUND
		;
		CMP	LAST_DELIM,0DH
		JMP	L41$

L4$:
		;
		;CR FOUND
		;
		CMP	LAST_DELIM,','
L41$:
		MOV	LAST_DELIM,0
		JNZ	L45$
if	fg_td
		BITT	TLINK_DEFAULT_MODE
		JNZ	L45$
endif
		JMP	L1$

L45$:
		MOV	LAST_DELIM,AL
		BITT	PLUS_FOUND
		PUSHFD
		RESS	PLUS_FOUND
		POPFD
		JZ	L71$
if	fg_td
		CMP	AL,0DH			;PLUS OVERIDES A CR ALWAYS
		JZ	L1$
		BITT	TLINK_DEFAULT_MODE	;IN TLINK MODE, THAT'S ALL IT OVERIDES
		JNZ	L71$
endif
		JMP	L1$

L71$:
		RET

L9$:
		RESS	PLUS_FOUND
		RET

PARSE_LIBS	ENDP


PARSE_RCS	PROC	NEAR
		;
		;GET NAMES OF .RES FILES TO BIND
		;
		MOV	FILESTUFF_PTR,OFF RESSTUFF
L1$:
		CALL	GETNST_SLASH
		CMP	AL,','
		JZ	L6$			;SKIP COMMA
		CMP	AL,0DH
		JZ	L4$			;TERMINATE IF PLUS NOT FOUND
		MOV	LAST_DELIM,0
		CMP	AL,'+'
		JZ	L3$			;FLAG + FOUND
		DEC	INPTR1
		CMP	AL,';'
		JZ	L9$
		CMP	AL,1AH
		JZ	L9$
		RESS	PLUS_FOUND
		SETT	RC_SUPPLIED
		MOV	EAX,OFF RESSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		CALL	HANDLE_RCS
		JMP	L1$

L3$:
		SETT	PLUS_FOUND
		JMP	L1$

L6$:
		;
		;COMMA FOUND
		;
		CMP	LAST_DELIM,0DH
		JMP	L41$

L4$:
		;
		;CR FOUND
		;
		CMP	LAST_DELIM,','
L41$:
		MOV	LAST_DELIM,0
		JNZ	L45$
if	fg_td
		BITT	TLINK_DEFAULT_MODE
		JNZ	L45$
endif
		JMP	L1$

L45$:
		MOV	LAST_DELIM,AL
		BITT	PLUS_FOUND
		PUSHFD
		RESS	PLUS_FOUND
		POPFD
		JZ	L71$
if	fg_td
		CMP	AL,0DH			;PLUS OVERIDES A CR ALWAYS
		JZ	L1$
		BITT	TLINK_DEFAULT_MODE	;IN TLINK MODE, THAT'S ALL IT OVERIDES
		JNZ	L71$
endif
		JMP	L1$

L71$:
		RET

L9$:
		RESS	PLUS_FOUND
		RET

PARSE_RCS	ENDP


MAKE_MAPOUT	PROC
		;
		PUSHM	EDI,ESI
		SETT	MAPFILE_SELECTED
		SETT	SYMBOLS_OUT
L1$:
		CALL	FIX_GET_COLON
		JNZ	L3$
		CALL	GET_KEYWORD
		MOV	EAX,SYMBOL_LENGTH	;AT LEAST THIS MANY MUST MATCH
		MOV	ESI,OFF FULL_TEXT
		MOV	ECX,EAX
		MOV	EDI,OFF SYMBOL_TEXT
		TEST	ECX,ECX
		MOV	EDX,EDI
		JZ	L4$
		REPE	CMPSB
		JNZ	L5$
		SETT	DETAILEDMAP_FLAG
		SETT	ALPHA_ORDER_ALSO
		JMP	L1$

L3$:
		POPM	ESI,EDI
		RET

L4$:
		POPM	ESI,EDI
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		RET

L5$:
		MOV	ECX,EAX			;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,EDX
		MOV	ESI,OFF ADDRESS_TEXT
		REPE	CMPSB
		JNZ	L51$
		RESS	ALPHA_ORDER_ALSO
		JMP	L1$

L51$:
		MOV	ECX,EAX			;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,EDX
		MOV	ESI,OFF GROUP_TEXT
		REPE	CMPSB
		JNZ	L52$
		SETT	PRINT_GROUP
		JMP	L1$

L52$:
		MOV	ECX,EAX			;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,EDX
		MOV	ESI,OFF NOGROUP_TEXT
		REPE	CMPSB
		JNZ	L53$
		RESS	PRINT_GROUP
		JMP	L1$

L53$:
L59$:
		CALL	GET_NUMBER1
		TEST	EAX,EAX
		JNZ	L6$
		RESS	ALPHA_ORDER_ALSO
L6$:
		JMP	L1$

MAKE_MAPOUT	ENDP


GET_NUMBER	PROC
		;
		;MS FORMAT NUMBER
		;
		CALL	GET_KEYWORD

GET_NUMBER1	LABEL	PROC

		PUSHM	ESI,EBX
		MOV	ESI,OFF SYMBOL_TEXT
		XOR	EAX,EAX
		XOR	EBX,EBX
		XOR	ECX,ECX
		MOV	BL,[ESI]
		MOV	ECX,10
		SUB	BL,30H
		INC	ESI
		CMP	BL,CL
		JA	GN_ERROR
		OR	BL,BL
		JNZ	GET_DECIMAL
		MOV	BL,[ESI]
		INC	ESI
		MOV	CL,8
		CMP	BL,'X'
		JZ	GET_HEX
		CMP	BL,'x'
		JNZ	GET_OCTAL
GET_HEX:
		MOV	CL,16
G_LOOP:
		MOV	BL,[ESI]
		INC	ESI
GET_OCTAL:
		SUB	BL,30H
		CMP	BL,0AH
		JC	GET_DECIMAL
		SUB	BL,7
		CMP	BL,0AH
		JC	NUMBER_DONE
		CMP	BL,10H		;MUST BE 0A-0F
		JC	GET_DECIMAL
		CMP	BL,'a'-30H-7
		JC	NUMBER_DONE
		CMP	BL,'f'-30H-7
		JA	NUMBER_DONE
		SUB	BL,20H
GET_DECIMAL:
		CMP	BL,CL
		JNC	NUMBER_DONE

		MUL	ECX
		JC	L9$
		ADD	EAX,EBX
		JNC	G_LOOP
L9$:
GN_ERROR::
		MOV	AL,ILL_NUMBER_ERR
		CALL	ERR_SYMBOL_TEXT_ABORT

NUMBER_DONE:
		SUB	ESI,OFF SYMBOL_TEXT+1		;RESTORE CHARACTERS NOT PROCESSED
		POP	EBX
		SUB	ESI,SYMBOL_LENGTH
		ADD	INPTR1,ESI
		POP	ESI
		RET

GET_NUMBER	ENDP


GET_COLON_NUMBER16	PROC

		CALL	GET_COLON_NUMBER
		TEST	EAX,0FFFF0000H
		JNZ	GN_ERROR
		RET

GET_COLON_NUMBER16	ENDP


GET_NUMBER16	PROC

		CALL	GET_NUMBER
		TEST	EAX,0FFFF0000H
		JNZ	GN_ERROR
		RET

GET_NUMBER16	ENDP


GET_COLON_NUMBER	PROC
		;
		;
		;
		CALL	CHECK_COLON
		JMP	GET_NUMBER

GET_COLON_NUMBER	ENDP


MS_HELP		PROC
		;
		;PRINT HELP MESSAGE
		;PROCESS THROUGH OTHER TABLE...
		;
		MOV	EAX,OFF MS_CMD_PTRS
		JMP	HELP_PROC_1

MS_HELP		ENDP


SET_DEBUGALL	PROC
		;
		;
		;
		MOV	AL,-1
DBG_SET::
		OR	DEBUG_TYPES_SELECTED,AL
		RET

SET_DEBUGALL	ENDP


SET_DEBUGBROWSERDEFS	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_BROWSERDEFS
		JMP	DBG_SET

SET_DEBUGBROWSERDEFS	ENDP


SET_DEBUGBROWSERREFS	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_BROWSERREFS
		JMP	DBG_SET

SET_DEBUGBROWSERREFS	ENDP


SET_DEBUGCOVERAGES	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_COVERAGES
		JMP	DBG_SET

SET_DEBUGCOVERAGES	ENDP


SET_DEBUGLOCALS	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_LOCALS
		JMP	DBG_SET

SET_DEBUGLOCALS	ENDP


SET_DEBUGLINNUMS	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_LINNUMS
		JMP	DBG_SET

SET_DEBUGLINNUMS	ENDP


SET_DEBUGPUBLICS	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_PUBLICS
		JMP	DBG_SET

SET_DEBUGPUBLICS	ENDP


SET_DEBUGTYPES	PROC
		;
		;
		;
		MOV	AL,MASK FL_DEBUG_TYPES
		JMP	DBG_SET

SET_DEBUGTYPES	ENDP


RES_DEBUGALL	PROC
		;
		;
		;
		MOV	AL,0
DBG_CLEAR::
		AND	DEBUG_TYPES_SELECTED,AL
		SETT	FANCY_DEBUG
		RET

RES_DEBUGALL	ENDP


RES_DEBUGBROWSERDEFS	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_BROWSERDEFS
		JMP	DBG_CLEAR

RES_DEBUGBROWSERDEFS	ENDP


RES_DEBUGBROWSERREFS	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_BROWSERREFS
		JMP	DBG_CLEAR

RES_DEBUGBROWSERREFS	ENDP


RES_DEBUGCOVERAGES	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_COVERAGES
		JMP	DBG_CLEAR

RES_DEBUGCOVERAGES	ENDP


RES_DEBUGLOCALS	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_LOCALS
		JMP	DBG_CLEAR

RES_DEBUGLOCALS	ENDP


RES_DEBUGLINNUMS	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_LINNUMS
		JMP	DBG_CLEAR

RES_DEBUGLINNUMS	ENDP


RES_DEBUGPUBLICS	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_PUBLICS
		JMP	DBG_CLEAR

RES_DEBUGPUBLICS	ENDP


RES_DEBUGTYPES	PROC
		;
		;
		;
		MOV	AL,NOT MASK FL_DEBUG_TYPES
		JMP	DBG_CLEAR

RES_DEBUGTYPES	ENDP


HANDLE_DEBUGFILES	PROC
		;
		;
		;
if	any_overlays
		CALL	FIX_GET_COLON
		JNZ	L9$
else
		CALL	CHECK_COLON
endif
		SETT	FANCY_DEBUG
L1$:
		MOV	EAX,OFF OBJSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;THAT FILE...
		OR	[ECX].FILE_LIST_STRUCT.FILE_LIST_PLINK_FLAGS,MASK FL_DEBUG_COVERAGES+MASK FL_DEBUG_LINNUMS+MASK FL_DEBUG_LOCALS+ \
						MASK FL_DEBUG_PUBLICS+MASK FL_DEBUG_TYPES+MASK FL_DEBUG_BROWSERDEFS+ \
						MASK FL_DEBUG_BROWSERREFS
		CALL	FIX_GET_COLON
		JZ	L1$
		RET

if	any_overlays
L9$:
		SETT	DEBUG_FLAG
		MOV	DEBUG_BYTE,'Y'
		RET
endif

HANDLE_DEBUGFILES	ENDP


HANDLE_DEBUGMODULES	PROC
		;
		;
		;
if	any_overlays
		CALL	FIX_GET_COLON
		JNZ	L9$
else
		CALL	CHECK_COLON
endif
		SETT	FANCY_DEBUG
L1$:
		CALL	YYLEX_SYMBOL		;

		MOV	EAX,OFF SYMBOL_TPTR
		CALL	FORCE_INSTALL_MODULE	;THAT FILE...
		;
		;
		;
		OR	[ECX].MODULE_STRUCT._M_FLAGS,MASK M_DEBUG_GLOBAL
		CALL	FIX_GET_COLON
		JZ	L1$
		RET

if	any_overlays
L9$:
		SETT	DEBUG_FLAG
		MOV	DEBUG_BYTE,'Y'
		RET
endif

HANDLE_DEBUGMODULES	ENDP


SET_DETAILEDMAP	PROC

		SETT	SEGMENTS_MAP
		SETT	DETAILEDMAP_FLAG
		RET

SET_DETAILEDMAP	ENDP


RES_DETAILEDMAP	PROC

		RESS	DETAILEDMAP_FLAG
		RET

RES_DETAILEDMAP	ENDP


NO_MAKE_MAPOUT	PROC

		RESS	SYMBOLS_OUT
		RET

NO_MAKE_MAPOUT	ENDP

if	fg_segm OR fg_pe

XNOIGNORECASE_PROC	PROC
		;
		;
		;
		SETT	PRESERVE_IMPEXP_CASE
		RET

XNOIGNORECASE_PROC	ENDP


XUPPER_PROC	PROC
		;
		;
		;
		RESS	PRESERVE_IMPEXP_CASE
		RET

XUPPER_PROC	ENDP


ERRORFLAG_PROC	PROC
		;
		;
		;
		SETT	HANDLE_EXE_ERRORFLAG
		RET

ERRORFLAG_PROC	ENDP


NOERRORFLAG_PROC	PROC
		;
		;
		;
		RESS	HANDLE_EXE_ERRORFLAG
		RET

NOERRORFLAG_PROC	ENDP

endif

SET_ENTRYPOINT	PROC
		;
		;
		;
		CALL	CHECK_COLON

		CALL	GET_SYMBOL	;PARSE A SYMBOL (NO TRANSLATE)

		MOV	EAX,SYMBOL_LENGTH
		PUSH	EDI

		MOV	ESI,OFF SYMBOL_TEXT
		MOV	ECX,EAX

		ADD	EAX,4
		PUSH	ESI

		P1ONLY_POOL_ALLOC

		MOV	ENTRYPOINT_TXT,EAX
		MOV	[EAX],ECX

		LEA	EDI,[EAX+4]

		REP	MOVSB

		POPM	ESI,EDI

		RET

SET_ENTRYPOINT	ENDP


if	fg_segm

FIXDS_PROC	PROC
		;
		;
		;
		SETT	FIXDS_FLAG
		RET

FIXDS_PROC	ENDP


RES_FIXDS	PROC
		;
		;
		;
		RESS	FIXDS_FLAG
		RET

RES_FIXDS	ENDP

endif

MAKE_XREF	PROC
		;
		SETT	XREF_OUT
		RET

MAKE_XREF	ENDP


NO_MAKE_XREF	PROC

		RESS	XREF_OUT
		RET

NO_MAKE_XREF	ENDP


CHECK_COLON	PROC

		CALL	FIX_GET_COLON
		JNZ	COLON_EXP
		RET

COLON_EXP	LABEL	PROC

		MOV	AL,MISSING_COLON_ERR
		JMP	ERR_SYMBOL_TEXT_ABORT

CHECK_COLON	ENDP


SET_ABORT_CHK	PROC

		SETT	ABORT_ON_CHECKSUM
		RET

SET_ABORT_CHK	ENDP


RES_ABORT_CHK	PROC

		RESS	ABORT_ON_CHECKSUM
		RET

RES_ABORT_CHK	ENDP


ENABLE_CHECKEXE	LABEL	PROC

;		SETT	CHECKSUM_EXE_FLAG
;		RET
;
DISABLE_CHECKEXE	PROC

;		RESS	CHECKSUM_EXE_FLAG
		RET

DISABLE_CHECKEXE	ENDP


SET_PAUSE	PROC

		SETT	PAUSE_FLAG
		RET

SET_PAUSE	ENDP


RES_PAUSE	PROC

		RESS	PAUSE_FLAG
		RET

RES_PAUSE	ENDP


SET_PROMPT_MODE	PROC

		RESS	CMDLINE_FINAL
		RET

SET_PROMPT_MODE	ENDP


RES_PROMPT_MODE	PROC

		SETT	CMDLINE_FINAL
		RET

RES_PROMPT_MODE	ENDP


ONERROR_PROC	PROC
		;
		;ONERROR:NOEXE
		;
		CALL	CHECK_COLON	;MUST HAVE A COLON
		CALL	GET_KEYWORD	;DOES UPPERCASE
		PUSHM	EDI,ESI

		MOV	EAX,SYMBOL_LENGTH
		MOV	EDI,OFF NOEXE_TXT
		TEST	EAX,EAX
		JZ	L9$
		MOV	ECX,EAX
		MOV	ESI,OFF SYMBOL_TEXT
		REPE	CMPSB		;CX BYTES MUST MATCH
		JNZ	L9$
		CALL	ERRORDELETE_PROC
		POPM	ESI,EDI
		RET

L9$:
		POPM	ESI,EDI
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		RET

ONERROR_PROC	ENDP


if	fg_segm

PMTYPE_PROC	PROC
		;
		;PMTYPE:[PM | VIO | NOVIO]
		;
		CALL	CHECK_COLON	;MUST HAVE A COLON
		CALL	GET_KEYWORD
		PUSHM	EDI,ESI,EBX

		MOV	EBX,OFF PM_TYPES
		MOV	EDX,3		;3 TYPES TO CHECK
L1$:
		MOV	ESI,[EBX]
		MOV	ECX,SYMBOL_LENGTH;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,OFF SYMBOL_TEXT
		TEST	ECX,ECX
		JZ	L4$
		REPE	CMPSB
		MOV	EAX,[EBX+4]
		JZ	L5$
		ADD	EBX,8
		DEC	EDX
		JNZ	L1$
L4$:
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		POPM	EBX,ESI,EDI
		RET

L5$:
		POPM	EBX,ESI,EDI
		OR	FLAG_0C,EAX
		RET

PMTYPE_PROC	ENDP

endif


EXETYPE_PROC	PROC
		;
		;EXETYPE:[OS2 | WINDOWS | DOS4 | UNKNOWN | DOS | NT | DOSX]
		;
		CALL	CHECK_COLON	;MUST HAVE A COLON
		CALL	GET_KEYWORD
		PUSHM	EDI,ESI,EBX
		MOV	EBX,OFF EXE_TYPES
		MOV	EDX,N_EXE_TYPES	;6 TYPES TO CHECK
L1$:
		MOV	ESI,[EBX]
		MOV	ECX,SYMBOL_LENGTH;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,OFF SYMBOL_TEXT
		TEST	ECX,ECX
		JZ	L4$
		REPE	CMPSB
		MOV	EAX,[EBX+4]
		JZ	L5$
		ADD	EBX,8
		DEC	EDX
		JNZ	L1$
L4$:
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		POPM	EBX,ESI,EDI
		RET

L5$:
		POPM	EBX,ESI,EDI
		MOV	EXETYPE_CMDLINE_FLAG,AL
		RET

EXETYPE_PROC	ENDP


SUBSYSTEM_PROC	PROC
		;
		;SUBSYSTEM:[OS2 | WINDOWS | DOS4 | UNKNOWN | DOS | NT | DOSX]
		;
		CALL	CHECK_COLON	;MUST HAVE A COLON
		CALL	GET_KEYWORD

		PUSHM	EDI,ESI,EBX
		MOV	EBX,OFF SUBSYS_TYPES
		MOV	EDX,N_SUBSYS_TYPES	;6 TYPES TO CHECK
L1$:
		MOV	ESI,[EBX]
		MOV	ECX,SYMBOL_LENGTH	;AT LEAST THIS MANY MUST MATCH
		MOV	EDI,OFF SYMBOL_TEXT
		TEST	ECX,ECX
		JZ	L4$

		REPE	CMPSB

		MOV	EAX,[EBX+4]
		JZ	L5$

		ADD	EBX,8
		DEC	EDX
		JNZ	L1$
L4$:
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		POPM	EBX,ESI,EDI
		RET

L5$:
		CALL	SET_SUBSYSTEM

		JNZ	L4$

		CALL	FIX_GET_COLON

		JNZ	L9$

		CALL	GET_NUM_DOT_NUM

		MOV	ECX,EDX
		CALL	SET_SUBSYS_VERSION

		JNZ	L4$
L9$:
		POPM	EBX,ESI,EDI

		RET

SUBSYSTEM_PROC	ENDP


FIX_GET_COLON	PROC
		;
		;
		;
		CALL	GETNST
		CMP	AL,':'
		JZ	L9$
		DEC	INPTR1
L9$:
		RET

FIX_GET_COLON	ENDP


SET_EMS_IO_LEGAL	PROC
		;
		SETT	EMS_IO_LEGAL
		RET

SET_EMS_IO_LEGAL	ENDP


RES_EMS_IO_LEGAL	PROC
		;
		RESS	EMS_IO_LEGAL
		RET

RES_EMS_IO_LEGAL	ENDP


SET_EMSMAXSIZE	PROC
		;
		CALL	GET_COLON_NUMBER16
;		MOV	EMS_MAX_1K_BLOCKS,AX
		RET

SET_EMSMAXSIZE	ENDP


SET_XMSMAXSIZE	PROC
		;
		CALL	GET_COLON_NUMBER16
;		MOV	XMS_MAX_1K_BLOCKS,AX
		RET

SET_XMSMAXSIZE	ENDP


SET_EMSUSE40	PROC
		;
		RESS	FORCE_EMS_32
		RET

SET_EMSUSE40	ENDP


RES_EMSUSE40	PROC
		;
		SETT	FORCE_EMS_32
		RET

RES_EMSUSE40	ENDP


SET_EXEPACK	PROC
		;
		;
		;
		SETT	EXEPACK_SELECTED
SET_EXEPACK_1	LABEL	PROC

		CALL	FIX_GET_COLON
		JZ	L1$
		RET

L1$:
		CALL	GET_NUMBER
		INC	EAX			;FORCE TO # OF WORDS...
		AND	AL,0FEH
		CMP	DONT_PACK,EAX
		JA	L2$
		MOV	DONT_PACK,EAX
L2$:
		RET

SET_EXEPACK	ENDP


RES_EXEPACK	PROC

		RESS	EXEPACK_SELECTED
		RET

RES_EXEPACK	ENDP


if	fg_winpack

SET_WINPACK	PROC
		;
		;
		;
if	fg_virt AND (NOT debug)
		MOV	AL,CANT_WINPACK_ERR
		push	EAX
		call	_err_abort
else
		SETT	WINPACK_SELECTED
endif

SET_WINPACK	ENDP


SET_SEGPACK	PROC
		;
		;
		;
		SETT	SEGPACK_SELECTED
		RET

SET_SEGPACK	ENDP


RES_WINPACK	PROC
		;
		;
		;
		RESS	WINPACK_SELECTED
		RESS	SEGPACK_SELECTED
		RET

RES_WINPACK	ENDP


SET_DEBUGAPPLOADER	PROC

		RESS	WINPACK_LICENSED
		RET

SET_DEBUGAPPLOADER	ENDP


RES_DEBUGAPPLOADER	PROC

		SETT	WINPACK_LICENSED
		RET

RES_DEBUGAPPLOADER	ENDP

endif

;SET_EXTDICTIONARY:
;		SETT	EXTDICTIONARY_FLAG
;		RET
RES_EXTDICTIONARY	PROC

;		RESS	EXTDICTIONARY_FLAG
		RET

RES_EXTDICTIONARY	ENDP


SET_LINENUMBERS	PROC

		SETT	MAPFILE_SELECTED
		SETT	LINENUMBERS_FLAG
		RET

SET_LINENUMBERS	ENDP


RES_LINENUMBERS	PROC

		RESS	LINENUMBERS_FLAG
		RET

RES_LINENUMBERS	ENDP


SET_DEFAULTLIBRARYSEARCH	PROC

		SETT	DEFAULTLIBRARYSEARCH_FLAG
		RET

SET_DEFAULTLIBRARYSEARCH	ENDP


RES_DEFAULTLIBRARYSEARCH	PROC
		;
		;
		;
		CALL	FIX_GET_COLON
		JZ	L1$
		RESS	DEFAULTLIBRARYSEARCH_FLAG
		RET

L1$:
		;
		;NEED TO DISABLE A SINGLE LIBRARY NAME...
		;OK,
		;
		SETT	DOING_NODEF
		MOV	EAX,OFF LIBSTUFF
		CALL	YYLEX_FILENAME		;NOW, WAS A PATH SUPPLIED?
		MOV	EAX,OFF FILNAM
		push	EAX
		call	_do_search_library
		add	ESP,4
		RESS	DOING_NODEF
		RET

RES_DEFAULTLIBRARYSEARCH	ENDP


if	fg_segm OR fg_pe

RC_PROC		PROC
		;
		;HANDLE SUPPLYING AN RC FILE FOR INCLUSION...
		;
		BITT	RC_SUPPLIED
		JNZ	L0$
		SETT	RC_SUPPLIED
		SETT	RC_REORDER
		OR	FLAG_0C,MASK APPWINFLAGS
L0$:
		CALL	FIX_GET_COLON
		JZ	L1$
		CMP	AL,'-'
		JZ	L2$
		RET

L9$:
		MOV	AL,MULTIPLE_RC_ERR
		push	EAX
		call	_err_abort

L1$:
		MOV	EAX,OFF RCSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		CALL	HANDLE_RCS
		JMP	L0$

L2$:
		INC	INPTR1
		CALL	GET_KEYWORD
		MOV	ECX,SYMBOL_LENGTH
		MOV	EAX,DPTR SYMBOL_TEXT
		DEC	ECX
		JNZ	L25$
		CMP	AL,'a'
		JC	L11$
		CMP	AL,'z'
		JA	L11$
		SUB	AL,20H
L11$:
		CMP	AL,'E'
		MOV	EBX,MASK APP386			;DRIVER USES EMS
		JZ	L19$
		CMP	AL,'L'
		MOV	EBX,MASK APP86			;APP USES EMS
		JZ	L19$
		CMP	AL,'M'
		MOV	EBX,MASK APP286			;MULTIPLE INSTANCE (MORE EMS JUNK)
		JZ	L19$
		CMP	AL,'P'
		MOV	EBX,MASK APP??			;PRIVATE LIBRARY
		JZ	L19$
		CMP	AL,'T'				;PROTMODE ONLY
		JZ	L16$
		CMP	AL,'K'
		JNZ	L95$
		RESS	RC_REORDER
		JMP	L0$

L16$:
		CALL	RC_PROTMODE
		JMP	L0$

L19$:
		OR	FLAG_0C,EBX
		JMP	L0$

L95$:
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		RET

L25$:
		DEC	ECX
		JNZ	L95$
		CMP	AX,'04'
		MOV	EBX,400H
		JZ	L27$
		CMP	AX,'03'
		MOV	BH,3
		JZ	L27$
		CMP	AX,'13'
		JNZ	L95$
		MOV	BL,0AH
L27$:
		BITT	WINVER_SELECTED
		JNZ	L28$
		MOV	NEXEHEADER._NEXE_WINVER_INT,BH
		SETT	WINVER_SELECTED
		MOV	NEXEHEADER._NEXE_WINVER_FRAC,BL
L28$:
		JMP	L0$

RC_PROC		ENDP


STUB_PROC	PROC
		;
		;HANDLE SUPPLYING A STUB FILE FOR INCLUSION...
		;
		CALL	CHECK_COLON

		MOV	EAX,OFF STUBSTUFF
		CALL	YYLEX_FILENAME

		BITT	STUB_SUPPLIED
		JNZ	L9$
		BITT	STUB_NONE
		JNZ	L9$

		MOV	EAX,OFF FILNAM.NFN_TEXT

		ADD	EAX,FILNAM.NFN_PATHLEN

		MOV	EAX,[EAX]

		CMP	EAX,'ENON'
		JZ	L5$

		CMP	EAX,'enon'
		JZ	L5$

		SETT	STUB_SUPPLIED

		MOV	EAX,OFF FILNAM
		JMP	HANDLE_STUB

L5$:
		SETT	STUB_NONE
		RET

L9$:
		MOV	AL,DUP_STUB_ERR
		JMP	ERR_INBUF_RET

STUB_PROC	ENDP


if	V5

		PUBLIC	PARSE_IMPDEF_FILENAME,PARSE_IMPLIB_FILENAME,PARSE_IMPLIB_COFF_FILENAME

SET_IMPDEF	PROC
		;
		;POSSIBLY :FILENAME, .DIN OUTPUT DESCRIBING THIS .DLL
		;
		SETT	IMPDEF_FLAG			;HE WANTS US TO BUILD .DIN
		CALL	FIX_GET_COLON
		JZ	L1$
		RET

L1$:
		CALL	PARSE_IMPDEF_FILENAME
		RET

SET_IMPDEF	ENDP


PARSE_IMPDEF_FILENAME	PROC
		;
		;
		;
		MOV	EAX,OFF IMPDEF_STUFF	;LIKE DEF, BUT USE PRIMARY NAME AS DEFAULT
		CALL	YYLEX_FILENAME
		SETT	SKIP_OUTFILE_LINK
		MOV	EAX,OFF FILNAM
		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;AX IS GINDEX, DS:BX IS FILE_LIST PHYS

		CALL	INSTALL_OUTFILE		;THIS IS AN OUTPUT FILE
		MOV	IMPDEF_FILE_GINDEX,EAX
		RESS	SKIP_OUTFILE_LINK
		RET

PARSE_IMPDEF_FILENAME	ENDP


SET_IMPLIB	PROC
		;
		;POSSIBLY :FILENAME, .LIB OUTPUT DESCRIBING THIS .DLL
		;
		SETT	IMPLIB_FLAG			;HE WANTS US TO BUILD .LIB
		CALL	FIX_GET_COLON
		JZ	L1$
		RET

L1$:
		CALL	PARSE_IMPLIB_FILENAME
		RET

SET_IMPLIB	ENDP


PARSE_IMPLIB_FILENAME	PROC
		;
		;
		;
		MOV	EAX,OFF IMPLIB_STUFF	;LIKE LIB, BUT USE PRIMARY NAME AS DEFAULT
		CALL	YYLEX_FILENAME
		SETT	SKIP_OUTFILE_LINK
		MOV	EAX,OFF FILNAM
		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;AX IS GINDEX, DS:BX IS FILE_LIST PHYS
		CALL	INSTALL_OUTFILE		;THIS IS AN OUTPUT FILE
		MOV	IMPLIB_FILE_GINDEX,EAX
		RESS	SKIP_OUTFILE_LINK
		RET

PARSE_IMPLIB_FILENAME	ENDP

SET_IMPLIB_COFF	PROC
		;
		;POSSIBLY :FILENAME, .LIB OUTPUT DESCRIBING THIS .DLL
		;
		SETT	IMPLIB_COFF_FLAG		;HE WANTS US TO BUILD .LIB
		CALL	FIX_GET_COLON
		JZ	L1$
		RET

L1$:
		CALL	PARSE_IMPLIB_COFF_FILENAME
		RET

SET_IMPLIB_COFF	ENDP

PARSE_IMPLIB_COFF_FILENAME PROC

		MOV	EAX,OFF IMPLIB_STUFF	;LIKE LIB, BUT USE PRIMARY NAME AS DEFAULT
		CALL	YYLEX_FILENAME
		SETT	SKIP_OUTFILE_LINK
		MOV	EAX,OFF FILNAM
		push	ECX
		mov	ECX,ESP
		push	ECX
		push	EAX
		call	_filename_install
		add	ESP,8
		pop	ECX
		;CALL	FILENAME_INSTALL	;AX IS GINDEX, DS:BX IS FILE_LIST PHYS
		CALL	INSTALL_OUTFILE		;THIS IS AN OUTPUT FILE
		MOV	IMPLIB_COFF_FILE_GINDEX,EAX
		RESS	SKIP_OUTFILE_LINK
		RET

PARSE_IMPLIB_COFF_FILENAME	ENDP



SET_PAGESIZE	PROC
		;
		;:nnn, SETS PAGESIZE FOR OPTIMP .LIB FILE
		;
		CALL	GET_COLON_NUMBER16

		MOV	IMPLIB_PAGESIZE,EAX

		RET

SET_PAGESIZE	ENDP

endif

endif

SET_CPARMAXALLOC	PROC

		CALL	GET_COLON_NUMBER16
		MOV	EXEHEADER._EXE_MAX_ABOVE,AX
		RET

SET_CPARMAXALLOC	ENDP

if	any_overlays
SET_OVERLAYINTERRUPT	PROC

		CALL	GET_COLON_NUMBER16
		MOV	OVERLAY_INTERRUPT_NUMBER,EAX
		RET

SET_OVERLAYINTERRUPT	ENDP
endif


SET_OPTLINK	PROC
		RET
SET_OPTLINK	ENDP


SET_NODOSSEG	PROC

		SETT	NODOSSEG_FLAG
		RET

SET_NODOSSEG	ENDP


SET_GROUPASSOCIATION	PROC

		SETT	GROUPASSOCIATION_FLAG
		RET

SET_GROUPASSOCIATION	ENDP


RES_GROUPASSOCIATION	PROC

		RESS	GROUPASSOCIATION_FLAG
		RET

RES_GROUPASSOCIATION	ENDP


SET_CODEVIEW	PROC
if	fg_cv
		SETT	CODEVIEW_FLAG
if	any_overlays
		MOV	CODEVIEW_BYTE,'Y'
endif
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER16
		MOV	CV_INFO_TYPE,AL
L9$:
endif
if	fg_td
		CALL	RES_TD
endif
		RET

SET_CODEVIEW	ENDP


SET_CVVERSION	PROC
		;
		;
		;
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER16
		MOV	CV_INFO_TYPE,AL
L9$:
		RET

SET_CVVERSION	ENDP


SET_CVWARNINGS	PROC
		;
		;
		;
		SETT	CV_WARNINGS

		RET

SET_CVWARNINGS	ENDP


RES_CODEVIEW	PROC
		;
		RESS	CODEVIEW_FLAG
if	any_overlays
		MOV	CODEVIEW_BYTE,'N'
endif
		RET

RES_CODEVIEW	ENDP


RES_CVPACK	PROC

		RESS	CVPACK_FLAG
		RET

RES_CVPACK	ENDP


SET_COMDEF_SEARCH	PROC
		;
		SETT	COMDEF_SEARCH
		RET

SET_COMDEF_SEARCH	ENDP


RES_COMDEF_SEARCH	PROC
		;
		RESS	COMDEF_SEARCH
		RET

RES_COMDEF_SEARCH	ENDP


SET_TD		PROC
if	fg_td
		SETT	TD_FLAG
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER16
		MOV	TD_INFO_TYPE,AL
L9$:
endif

if	fg_cv
		CALL	RES_CODEVIEW
endif
		RET

SET_TD		ENDP


if	fg_td

SET_TDVERSION	PROC
		;
		;
		;
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER16
		MOV	TD_INFO_TYPE,AL
L9$:
		RET

SET_TDVERSION	ENDP

endif

RES_TD		PROC
if	fg_td
		RESS	TD_FLAG
endif
		RET

RES_TD		ENDP


if	fg_td
		PUBLIC	TLINK_HELP

TLINK_HELP	PROC
		;
		;PRINT TLINK HELP SCREEN AND TERMINATE
		;
		CALL	FORCE_SIGNON
		MOV	EAX,OFF TLINK_HELP_MSG
		MOV	ECX,LENGTH_TLINK_HELP_MSG
		CALL	LOUTALL_CON
		CALL	_abort
		RET

TLINK_HELP	ENDP

A		EQU	<>
B		EQU	<0dh,0ah,>

TLINK_HELP_MSG	DB	'/3       - ignored                  ',A \
			'/A=n     - set alignment size       ',B \
			'/C[+|-]  - case on exports          ',A \
			'/E       - ignored                  ',B \
			'/Gn      - delete nonresident names ',A \
			'/Gr      - all exports by ordinal   ',B \
			'/Lpath   - library search paths     ',A \
			'/M       - add publics to .MAP      ',B \
			'/OPT     - select OPTLINK syntax    ',A \
			'/P[=n|-] - control packcode         ',B \
			'/Pc      - control packcode         ',A \
			'/Pd      - control packdata         ',B \
			'/Td[c|e] - select DOS output type   ',A \
			'/Tw[e|d] - select Windows output    ',0DH,0AH

		DB	'/Vt      - ignored                  ',A \
			'/Wd      - create IMPDEF file       ',B \
			'/Wi      - verbose linker output    ',A \
			'/Wl      - create IMPLIB file       ',B \
			'/Wo      - reorder segments         ',A \
			'/Wp      - WINPACK executable       ',B \
			'/Wr      - Bind .RES file           ',A \
			'/Ws=n    - PAGESIZE for IMPLIB      ',B \
			'/Wx      - add XREF to .MAP         ',0DH,0AH

		DB	'/c       - case significance        ',A \
			'/d       - warn dup library symbols ',B \
			'/e       - ignored                  ',A \
			'/f       - disable farcalltranslate ',B \
			'/i       - uninitialized vars==0    ',A \
			'/l       - add linnums to .MAP      ',B \
			'/m       - add publics to .MAP      ',A \
			'/n       - ignore obj lib requests  ',B \
			'/s       - detailed segment .MAP    ',A \
			'/t       - select .COM output       ',B \
			'/v       - force TD output for all  ',A \
			'/x       - disable .MAP output      ',B \
			'/ye[+|-|n] - control EMS usage      ',A \
			'/yx[+|-|n] - control XMS usage      ',0DH,0AH

		DB	'/v+|-    - enable/disable all debug for subsequent files',0dh,0ah,\
			'/vb[+|-] - enable/disable browser data for subsequent files',0dh,0ah,\
			'/vc[+|-] - enable/disable coverage data for subsequent files',0dh,0ah,\
			'/vl[+|-] - enable/disable local symbols for subsequent files',0dh,0ah,\
			'/vL[+|-] - enable/disable line numbers for subsequent files',0dh,0ah,\
			'/vp[+|-] - enable/disable public symbols for subsequent files',0dh,0ah,\
			'/vt[+|-] - enable/disable typing info for subsequent files',0dh,0ah

LENGTH_TLINK_HELP_MSG	EQU	$-TLINK_HELP_MSG


SET_TLINK	PROC
		;
		;WE ARE ABOUT TO SWITCH TO TLINK SYNTAX...  HOLD ON TO YOUR HATS...
		;
		;IF THIS IS THE FIRST SWITCH PROCESSED IN THE .CFG FILE:
		;	1. SELECTS TLINK DEFAULT FLAGS AND VALUES
		;	2. AUTOTERMINATES COMMAND LINE, I.E., NO PROMPTING AND NO ';' REQUIRED
		;	3. EMPTY COMMAND LINE MEANS TLINK-HELP SCREEN
		;
		BITT	SLASH_FOUND
		JNZ	L5$
		BITT	DOING_CFG
		JZ	L5$
		MOV	EAX,OFF TFLAGS
		CALL	MOVE_DEFAULT_FLAGS
		CALL	SET_CASE_MODE			;SET DEFAULT CASE SIGNIFICANCE MODE
		SETT	TLINK_DEFAULT_MODE
L5$:
		SETT	TLINK_SYNTAX
		RET

SET_TLINK	ENDP

endif

if	any_overlays
RES_MODEL_FAR	PROC
		;

		MOV	MODEL_FAR_BYTE,'N'
		RET

RES_MODEL_FAR	ENDP

endif


BATCH_PROC	PROC
		;
		;ABORT ON FILE-NOT-FOUND
		;
		SETT	BATCH_FLAG
		CALL	RES_PROMPT_MODE
		RET

BATCH_PROC	ENDP


RES_BATCH	PROC
		;
		RESS	BATCH_FLAG
		CALL	SET_PROMPT_MODE
		RET

RES_BATCH	ENDP


SET_BINARY	PROC
		;
		SETT	OUTPUT_COM_SYS
		SETT	OUTPUT_SYS
		XOR	EAX,EAX
		PUSH	EAX
		CALL	FIX_GET_COLON
		POP	EAX
		JNZ	SB_DONE
		CALL	GET_NUMBER

SB_DONE		LABEL	PROC

		MOV	SYS_BASE,EAX
		RET


SET_TINY	LABEL	PROC

		SETT	OUTPUT_COM_SYS
		SETT	OUTPUT_COM
		MOV	EAX,100H
		JMP	SB_DONE

SET_BINARY	ENDP


if	any_overlays
SET_RELOAD	PROC
		;
		SETT	DOING_RELOAD
		MOV	RELOAD_BYTE,'Y'
		CALL	GET_COLON_NUMBER16
		MOV	RELOAD_DEPTH,EAX
		RET

SET_RELOAD	ENDP
endif


SET_RELOCATION_CHECK	PROC
		;
		SETT	CHECK_RELOCATIONS
		RET

SET_RELOCATION_CHECK	ENDP


RES_RELOCATION_CHECK	PROC

		RESS	CHECK_RELOCATIONS
		RET

RES_RELOCATION_CHECK	ENDP


SET_FARCALLTRANSLATION	PROC
		;
		RESS	NOFARCALLTRANSLATION_FLAG
		SETT	FARCALLTRANSLATION_FLAG
		RET

SET_FARCALLTRANSLATION	ENDP


RES_FARCALLTRANSLATION	PROC
		;
		SETT	NOFARCALLTRANSLATION_FLAG
		RESS	FARCALLTRANSLATION_FLAG
		RET

RES_FARCALLTRANSLATION	ENDP


SET_PACKCODE	PROC

		RESS	NOPACKCODE_FLAG
		SETT	PACKCODE_FLAG
		MOV	EAX,OFF PACKCODE
		JMP	PACK_PROC

SET_PACKCODE	ENDP


RES_PACKCODE	PROC
		;
		RESS	PACKCODE_FLAG
		SETT	NOPACKCODE_FLAG
		RET

RES_PACKCODE	ENDP


SET_PACKDATA	PROC

		RESS	NOPACKDATA_FLAG
		SETT	PACKDATA_FLAG
		MOV	EAX,OFF PACKDATA

PACK_PROC	LABEL	PROC

		PUSH	EAX
		CALL	FIX_GET_COLON
		JZ	L1$
		MOV	EAX,PACK_DEFAULT_SIZE
		JMP	L3$

L1$:
		CALL	GET_NUMBER		;EAX
		CMP	EAX,64K
		JA	GN_ERROR
		TEST	EAX,EAX
		JNZ	L3$
		MOV	EAX,64K
L3$:
		POP	ECX
		MOV	DPTR [ECX],EAX
		RET

SET_PACKDATA	ENDP


RES_PACKDATA	PROC
		;
		RESS	PACKDATA_FLAG
		SETT	NOPACKDATA_FLAG
		RET

RES_PACKDATA	ENDP


PACKSIZE_PROC	PROC
		;
		;
		;
		CALL	GET_COLON_NUMBER16
		TEST	EAX,EAX
		JNZ	L1$
		MOV	EAX,64K
L1$:
		MOV	PACK_DEFAULT_SIZE,EAX
		RET

PACKSIZE_PROC	ENDP


SET_PACKCODE_NO_SEGMENTS	PROC
		;
		SETT	PACKCODE_NO_SEGMENTS
		RET

SET_PACKCODE_NO_SEGMENTS	ENDP


RES_PACKCODE_NO_SEGMENTS	PROC
		;
		RESS	PACKCODE_NO_SEGMENTS
		RET

RES_PACKCODE_NO_SEGMENTS	ENDP


SET_NTHOST	PROC
		;
		;
		;
		SETT	NT_HOST_FLAG
		RET

SET_NTHOST	ENDP


RES_NTHOST	PROC
		;
		;
		;
		RESS	NT_HOST_FLAG
		RET

RES_NTHOST	ENDP


SET_NULLSDOSSEG	PROC
		;
		SETT	NULLSDOSSEG_FLAG
		CALL	DOSSEG_PROC
		RET

SET_NULLSDOSSEG	ENDP


SET_SCANLIB	PROC
		;
		;
		;
		SETT	SCAN_LIB_ENVIRONMENT
		RET

SET_SCANLIB	ENDP


RES_SCANLIB	PROC
		;
		;
		;
		RESS	SCAN_LIB_ENVIRONMENT
		RET

RES_SCANLIB	ENDP


SET_SCANLINK	PROC
		;
		;
		;
		SETT	SCAN_LINK_ENVIRONMENT
		RET

SET_SCANLINK	ENDP


RES_SCANLINK	PROC
		;
		;
		;
		RESS	SCAN_LINK_ENVIRONMENT
		RET

RES_SCANLINK	ENDP


if	fg_segm

SET_WARNFIXUP	PROC
		;
		SETT	WARNFIXUP
		RET

SET_WARNFIXUP	ENDP

endif


SET_WARNDUPS	PROC

		SETT	WARN_LIB_DUPS
		RET

SET_WARNDUPS	ENDP


RES_WARNDUPS	PROC

		RESS	WARN_LIB_DUPS
		RET

RES_WARNDUPS	ENDP


if	fg_segm OR fg_pe

SET_PADDATA	PROC
		;
		CALL	GET_COLON_NUMBER16
		MOV	PADDATA,EAX
		RET

SET_PADDATA	ENDP


SET_PADCODE	PROC
		;
		CALL	GET_COLON_NUMBER16
		MOV	PADCODE,EAX
		RET

SET_PADCODE	ENDP

endif


SET_STACK	PROC
		;
		CALL	GET_COLON_NUMBER
		CALL	SET_STACK_1
		;
if	fg_pe
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER
		MOV	PEXEHEADER._PEXE_STACK_COMMIT,EAX
endif

L9$:
		RET

SET_STACK	ENDP


SET_STACK_1	PROC

		ADD	EAX,3				;DWORD ALIGN IT...
		AND	AL,0FCH
		SETT	STACK_SIZE_FLAG
		MOV	STACK_SIZE,EAX
		RET

SET_STACK_1	ENDP


if	fg_segm OR fg_pe

SET_HEAP	PROC
		;
		CALL	GET_COLON_NUMBER
		CALL	SET_HEAP_1
		;
if	fg_pe
		CALL	FIX_GET_COLON
		JNZ	L9$
		CALL	GET_NUMBER
		MOV	PEXEHEADER._PEXE_HEAP_COMMIT,EAX
endif

L9$:
		RET

SET_HEAP	ENDP


SET_HEAP_1	PROC

		ADD	EAX,3				;DWORD ALIGN IT...
		AND	AL,0FCH
		SETT	HEAP_SIZE_FLAG
		MOV	HEAP_SIZE,EAX
		RET

SET_HEAP_1	ENDP

endif

if	fg_pe

SET_PE_BASE	PROC
		;
		;
		;
		CALL	GET_COLON_NUMBER
		MOV	PE_BASE,EAX
		SETT	PE_BASE_SPECIFIED
		RET

SET_PE_BASE	ENDP


SET_PE_FIXED	PROC
		;
		;
		;
		SETT	PE_BASE_FIXED
		RET

SET_PE_FIXED	ENDP


SET_MACHINE	PROC
		;
		;ONLY SUPPORTS i386, i486, PENTIUM
		;
		CALL	CHECK_COLON
		CALL	GET_KEYWORD
		PUSHM	EDI,ESI
		MOV	ECX,SYMBOL_LENGTH	;AT LEAST THIS MANY MUST MATCH
		MOV	ESI,OFF I386_TXT
		TEST	ECX,ECX
		MOV	EDI,OFF SYMBOL_TEXT
		JZ	L4$
		REPE	CMPSB
		JNZ	L4$
		POPM	ESI,EDI
if	fg_pe
		MOV	PEXEHEADER._PEXE_CPU_REQUIRED,PEXE_80386
endif
		RET

L4$:
		POPM	ESI,EDI
		MOV	AL,CMDLIN_ERR
		CALL	ERR_INBUF_RET
		RET

SET_MACHINE	ENDP


SET_VERSION	PROC
		;
		;
		;
		CALL	CHECK_COLON
		CALL	GET_NUM_DOT_NUM
if	fg_pe
		MOV	PEXEHEADER._PEXE_USER_MAJOR,AX
		MOV	PEXEHEADER._PEXE_USER_MINOR,DX
endif
		RET

SET_VERSION	ENDP

endif


if	fg_segm or fg_norm_exe OR fg_pe

SET_ALIGNMENT	PROC
		;
		CALL	GET_COLON_NUMBER
		MOV	ALIGNMENT,EAX		;MUST BE A POWER OF 2...
		SETT	ALIGNMENT_SPECIFIED
		RET

SET_ALIGNMENT	ENDP

endif


SET_SEGMENTS	PROC
		;
		CALL	GET_COLON_NUMBER	;IGNORED...
		RET

SET_SEGMENTS	ENDP


if	fg_dospack

SET_PACKDOTS	PROC
		;
		SETT	DOTS_FLAG
		RET

SET_PACKDOTS	ENDP


RES_PACKDOTS	PROC
		;
		RESS	DOTS_FLAG
		RET

RES_PACKDOTS	ENDP


SET_QUIKPACK	PROC
		;
		SETT	QUIKPACK_FLAG

SET_SLRPACK	LABEL	PROC

		SETT	SLRPACK_SELECTED
if	any_overlays
		MOV	COMPRESS_BYTE,'Y'
endif
		JMP	SET_EXEPACK_1

SET_QUIKPACK	ENDP

endif


if	fg_td

PARSE_TLINK_SWITCH	PROC	NEAR
		;
		;AL IS FIRST CHARACTER
		;
		PUSH	ESI
		CMP	AL,'a'
		MOV	ESI,OFF SYMBOL_TEXT+1
		MOV	ECX,SYMBOL_LENGTH
		JB	PTS_CAPS
		;
		;aa	- SUBSYSTEM:WINDOWS
		;ap	- SUBSYSTEM:CONSOLE
		;c	- /NOI
		;d	- /WARNDUPS
		;e	- /NOE
		;f	- /NOFARCALLTRANSLATE
		;i	- INITIALIZE ALL SEGMENTS
		;l	- /LI
		;m	- /MAP
		;n	- /NOD
		;o	- /OVERLAYS - NOT SUPPORTED
		;s	- /DETAILEDMAP
		;t	- /TINY
		;v	- /TURBO
		;x	- MAPFILE == NUL
		;ye	- EXPANDED
		;yx	- EXTENDED
		;
		CMP	AL,'n'
		JB	PTS_BELOW_n
		JZ	PTS_n
		CMP	AL,'v'
		JB	PTS_BELOW_v
		JZ	PTS_v
		CMP	AL,'x'
		JZ	PTS_x
		CMP	AL,'y'
		JNZ	PTS_??
		CALL	PROCESS_TLINK_y_SWITCH
		JC	PTS_??
		RET

PTS_x:
		SETT	TLINK_X_MAPFILE
		JMP	CHECK_1

PTS_v:
		CALL	PROCESS_TLINK_v_SWITCH
		JC	PTS_??
		RET

PTS_BELOW_v:
		CMP	AL,'s'
		JZ	PTS_s
		CMP	AL,'t'
		JNZ	PTS_??
		CALL	SET_TINY
		JMP	CHECK_1

PTS_s:
		CALL	SET_DETAILEDMAP
		JMP	CHECK_1

PTS_n:
		RESS	DEFAULTLIBRARYSEARCH_FLAG
		JMP	CHECK_1

PTS_BELOW_n:
		CMP	AL,'i'
		JB	PTS_BELOW_i
		JZ	PTS_i
		CMP	AL,'l'
		JZ	PTS_l
		CMP	AL,'m'
		JNZ	PTS_??
PTS_m:
		SETT	MAPFILE_SELECTED
		SETT	SYMBOLS_OUT
		JMP	CHECK_1

PTS_i:
		SETT	TLINK_INIT_SEGMENTS
		JMP	CHECK_1

PTS_l:
		CALL	SET_LINENUMBERS
CHECK_1:
		CMP	SYMBOL_LENGTH,1
		JZ	CHECK_19
		CALL	WARN_TLINK_XTRA
CHECK_19:
		RET

PTS_BELOW_i:
		CMP	AL,'a'
		JZ	PTS_a
		CMP	AL,'c'
		JZ	PTS_c
		CMP	AL,'d'
		JZ	PTS_d
		CMP	AL,'e'
		JZ	PTS_e
		CMP	AL,'f'
		JNZ	PTS_??
		CALL	RES_FARCALLTRANSLATION
		JMP	CHECK_1

PTS_e:
		JMP	CHECK_1			;IGNORE EXTENTED DICTIONARY

PTS_??:
		MOV	AL,CMDLIN_ERR
		CALL	WARN_SYMBOL_TEXT_RET
		RET

PTS_c:
		CALL	NOIGNORECASE_PROC
		JMP	CHECK_1

PTS_a:
		CALL	PROCESS_TLINK_a_SWITCH
		JC	PTS_??
		RET

PTS_d:
		CALL	RES_WARNDUPS
		JMP	CHECK_1

PTS_CAPS:
		;
		;? - HELP SCREEN
		;3 - 32-BIT SUPPORT
		;A - ALIGNMENT
		;C - CASE SENSITIVE EXPORTS
		;E - USE EXTENDED DICTIONARY
		;Gm - Use MANGLED NAMES IN MAP
		;Gn - DISCARD NONRESIDENT NAME TABLE
		;Gr - TREAT ALL EXPORTS AS EXPORT BY ORDINAL...
		;L - LIBRARY SEARCH PATHS
		;M - SAME AS m
		;OPT - SWITCH TO OPTLINK SYNTAX
		;P - PACK CODE SEGMENTS
		;Pc - SAME AS P
		;Pd - PACK DATA SEGMENTS
		;Td? - DOS OUTPUT TYPES
		;Tw? - WINDOWS OUTPUT TYPES
		;TLINK - IGNORE
		;Vt - COMPRESS TYPES
		;Wd	- /IMPDEF
		;Wl	- /IMPLIB
		;Wp	- /WINPACK
		;Wr	- /RC
		;Ws	- /PAGESIZE
		;
		CMP	AL,'L'
		JB	PTS_BELOW_L
		JZ	PTS_L
		CMP	AL,'P'
		JB	PTS_BELOW_P
		JZ	PTS_P
		CMP	AL,'T'
		JZ	PTS_T
		CMP	AL,'V'
		JZ	PTS_V
		CMP	AL,'W'
		JZ	PTS_W
		JMP	PTS_??

PTS_W:
if	V5
		CALL	PROCESS_TLINK_W_SWITCH
		JC	PTS_??
		RET
else
		JMP	PTS_??
endif

PTS_V:
		CALL	PROCESS_TLINK_V_SWITCH
		JC	PTS_??
		RET

PTS_T:
		CALL	PROCESS_TLINK_T_SWITCH
		JC	PTS_??
		RET

PTS_BELOW_L:
		;
		;?, 3, A, C, E, G
		;
		CMP	AL,'A'
		JB	PTS_BELOW_A
		JZ	PTS_A
		CMP	AL,'C'
		JZ	PTS_C
		CMP	AL,'E'
		JZ	PTS_E
		CMP	AL,'G'
		JNZ	PTS_??
		CALL	PROCESS_TLINK_G_SWITCH
		JC	PTS_??
		RET

PTS_E:
		JMP	CHECK_1

PTS_C:
		CALL	PROCESS_TLINK_C_SWITCH
		JC	PTS_??
		RET

PTS_BELOW_A:
		CMP	AL,'?'
		JZ	TLINK_HELP1
		CMP	AL,'3'
		JNZ	PTS_??
		JMP	CHECK_1			;IGNORE 386 MODE

TLINK_HELP1:
		JMP	TLINK_HELP

PTS_P:
		;
		;PACKCODE SWITCH, LOOK FOR POSSIBLE = OR -
		;
		CALL	PROCESS_TLINK_P_SWITCH
		JC	PTS_???
		RET

PTS_???:
		JMP	PTS_??

PTS_A:
		CALL	PROCESS_TLINK_A_SWITCH
		JC	PTS_???
		RET

PTS_L:
		CALL	PROCESS_TLINK_L_SWITCH
		JC	PTS_???
		RET

PTS_M:
		JMP	PTS_m

PTS_BELOW_P:
		;
		;M
		;OPT
		;
		CMP	AL,'M'
		JZ	PTS_M
		CMP	AL,'O'
		JNZ	PTS_???
		CMP	WPTR SYMBOL_TEXT+1,'TP'
		JNZ	PTS_???
		CMP	CX,3
		JB	PTS_???
		RESS	TLINK_SYNTAX		;GO BACK TO OPTLINK SYNTAX
		CMP	ECX,3
		JZ	PTS_OPT
		CALL	WARN_TLINK_XTRA
PTS_OPT:
		RET

PARSE_TLINK_SWITCH	ENDP


PROCESS_TLINK_a_SWITCH	PROC	NEAR
		;
		;aa OR ap
		;
		DEC	ECX
		JZ	L99$
		DEC	ECX
		JZ	L1$
		CALL	WARN_TLINK_XTRA
L1$:
		MOV	AL,[ESI]
		INC	ESI
		CMP	AL,'a'
		MOV	EDX,2
		JZ	L3$
		INC	EDX
		CMP	AL,'p'
		JNZ	L99$
L3$:
if	fg_pe
		MOV	PEXEHEADER._PEXE_SUBSYSTEM,DX
		SETT	PE_SUBSYSTEM_DEFINED
endif
		RET

L99$:
		STC
		RET

PROCESS_TLINK_a_SWITCH	ENDP


PROCESS_TLINK_y_SWITCH	PROC	NEAR
		;
		;ye OR yx
		;
		DEC	ECX
		JZ	L99$
		MOV	AL,[ESI]
		INC	ESI
		CMP	AL,'e'
		JZ	L5$
		CMP	AL,'x'
		JNZ	L99$
		;
		;yx, yx+, yx-, yxN
		;
		CALL	FINISH_YEX
;		MOV	XMS_MAX_1K_BLOCKS,AX
		CLC
		RET

L5$:
		;
		;ye, ye+, ye-, yeN
		;
		CALL	FINISH_YEX
;		MOV	EMS_MAX_1K_BLOCKS,AX
		CLC
		RET

L99$:
		STC
		RET

PROCESS_TLINK_y_SWITCH	ENDP


FINISH_YEX	PROC	NEAR
		;
		;ye, ye+, ye-, yeN
		;
		DEC	ECX
		JNZ	L2$
		CALL	SEE_NEXT		;SEE IF NEXT IS + OR -
		CMP	AL,'+'
		JZ	L4$
		CMP	AL,'-'
		JNZ	L45$
		CALL	GET_NEXT
		XOR	EAX,EAX
		RET

L2$:
		MOV	EAX,TOKSTR
		ADD	EAX,2			;SKIP yx
		MOV	INPTR1,EAX
		CALL	GET_NUMBER16
		RET

L4$:
		CALL	GET_NEXT
L45$:
		MOV	EAX,-1
		RET

FINISH_YEX	ENDP


PROCESS_TLINK_v_SWITCH	PROC
		;
		;/v, /v+, /v-, /vX[+ | - ], WHERE X IS:
		;		p	=	PUBLIC SYMBOLS
		;		l	=	LOCAL SYMBOLS - SCOPES
		;		L	=	LINENUMBERS
		;		t	=	TYPE INFO
		;		c	=	COVERAGE INFORMATION
		;		b	=	BROWSER DEFINITIONS
		;		r	=	BROWSER REFERENCES
		;
		DEC	ECX
		MOV	DL,-1			;ITS  ALL DEBUG TYPES
		JZ	L5$
		DEC	ECX
		JZ	L1$
		CALL	WARN_TLINK_XTRA
L1$:
		MOV	AL,[ESI]
		INC	ESI
		CMP	AL,'c'
		MOV	DL,MASK FL_DEBUG_COVERAGES
		JZ	L8$
		CMP	AL,'p'
		MOV	DL,MASK FL_DEBUG_PUBLICS
		JZ	L8$
		CMP	AL,'l'
		MOV	DL,MASK FL_DEBUG_LOCALS
		JZ	L8$
		CMP	AL,'L'
		MOV	DL,MASK FL_DEBUG_LINNUMS
		JZ	L8$
		CMP	AL,'t'
		MOV	DL,MASK FL_DEBUG_TYPES
		JZ	L8$
		CMP	AL,'b'
		MOV	DL,MASK FL_DEBUG_BROWSERDEFS
		JZ	L8$
		CMP	AL,'r'
		MOV	DL,MASK FL_DEBUG_BROWSERREFS
		JNZ	L6$
L8$:
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	L87$
		CMP	AL,'+'
		JNZ	L85$
L84$:
		CALL	GET_NEXT
		SETT	FANCY_DEBUG
L85$:
		OR	DEBUG_TYPES_SELECTED,DL
L86$:
		CALL	SELECT_A_DEBUGTYPE
		CLC
		RET

L87$:
		CALL	GET_NEXT
		NOT	DL
		SETT	FANCY_DEBUG
		AND	DEBUG_TYPES_SELECTED,DL
		CLC
		RET

L5$:
		CALL	SEE_NEXT
		CMP	AL,'+'
		JZ	L84$
		CMP	AL,'-'
		JZ	L87$
		SETT	DEBUG_ALL
		JMP	L85$

L6$:
		STC
		RET

PROCESS_TLINK_v_SWITCH	ENDP


PROCESS_TLINK_A_SWITCH	PROC	NEAR
		;
		;ALIGNMENT SWITCH
		;
		DEC	ECX
		JNZ	L99$
		CALL	GET_NEXT	;MUST BE '='
		CMP	AL,'='
		JNZ	L99$
		CALL	GET_NUMBER
		MOV	ALIGNMENT,EAX
		SETT	ALIGNMENT_SPECIFIED
		CLC
		RET

L99$:
		STC
		RET

PROCESS_TLINK_A_SWITCH	ENDP


PROCESS_TLINK_C_SWITCH	PROC	NEAR
		;
		;/C, POSSIBLY /C+ OR /C-
		;
		DEC	ECX
		JNZ	L99$
		CALL	SEE_NEXT
		CMP	AL,'+'
		JZ	L2$
		CMP	AL,'-'
		JNZ	L8$
L2$:
		CALL	GET_NEXT
		CMP	AL,'+'
		JZ	L8$
		RESS	PRESERVE_IMPEXP_CASE
		CLC
		RET

L8$:
		SETT	PRESERVE_IMPEXP_CASE
		CLC
		RET

L99$:
		STC
		RET

PROCESS_TLINK_C_SWITCH	ENDP


PROCESS_TLINK_G_SWITCH	PROC	NEAR
		;
		;Gm, Gn, OR Gr
		;
		MOV	AL,[ESI]
		INC	ESI
		CMP	ECX,2
		JNZ	L99$
		CMP	AL,'m'
		JZ	T_Gm
		CMP	AL,'n'
		JZ	T_Gn
		CMP	AL,'r'
		JZ	T_Gr
L99$:
		STC
		RET

T_Gm:
		JMP	L9$		;CURRENTLY ALL I SUPPORT IS MANGLED NAMES...

T_Gn:
		;
		;DISCARD NON-RESIDENT NAMES TABLE
		;
		CALL	SET_NONAMES
		JMP	L9$

T_Gr:
		;
		;TREAT ALL EXPORTS AS EXPORT-BY-ORDINAL
		;
		CALL	SET_BY_ORDINAL
L9$:
		CLC
		RET

PROCESS_TLINK_G_SWITCH	ENDP


PROCESS_TLINK_L_SWITCH	PROC	NEAR
		;
		;PARSE  LIBRARY SEARCH PATHS...
		;
		MOV	EAX,TOKSTR
		SETT	FORCE_PATH
		INC	EAX			;SKIP L
		MOV	INPTR1,EAX
L1$:
		;
		;WELL, WE SKIP ';', TERMINATE ON MOST ANYTHING ELSE NOT VALID IN A FILE NAME
		;
		CALL	SEE_NEXT
		CMP	AL,';'
		JZ	L7$
		CMP	AL,' '
		JBE	L9$
		CMP	AL,'/'
		JZ	L9$
		CMP	AL,'-'
		JZ	L9$

		MOV	EAX,OFF PATHSTUFF
		CALL	YYLEX_FILENAME
		MOV	EAX,OFF FILNAM
		CALL	HANDLE_LIBS
		JMP	L1$

L7$:
		CALL	GET_NEXT
		JMP	L1$

L9$:
		RESS	FORCE_PATH
		CLC
		RET

PROCESS_TLINK_L_SWITCH	ENDP


PROCESS_TLINK_P_SWITCH	PROC	NEAR
		;
		;/P, MAYBE /P-, OR MAYBE /P=SIZE
		;/Pc, SAME AS ABOVE
		;/Pd, PACKDATA, SAME AS ABOVE
		;
		DEC	ECX
		JZ	L1$
		DEC	ECX
		JNZ	L99$
		MOV	AL,[ESI]
		INC	ESI
		CMP	AL,'c'
		JZ	L1$
		CMP	AL,'d'
		JZ	L2$
		JMP	L99$

L1$:
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	L17$
		RESS	NOPACKCODE_FLAG
		SETT	PACKCODE_FLAG
		CMP	AL,'='
		JZ	L15$
		MOV	EAX,PACK_DEFAULT_SIZE
		CLC
		MOV	PACKCODE,EAX
		RET

L15$:
		CALL	GET_NEXT
		CALL	GET_NUMBER16
		MOV	PACKCODE,EAX
		CLC
		RET

L17$:
		CALL	GET_NEXT
		RESS	PACKCODE_FLAG
		SETT	NOPACKCODE_FLAG
		CLC
		RET

L99$:
		STC
		RET

L2$:
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	L27$
		RESS	NOPACKDATA_FLAG
		SETT	PACKDATA_FLAG
		CMP	AL,'='
		JZ	L25$
		MOV	EAX,PACK_DEFAULT_SIZE
		CLC
		MOV	PACKDATA,EAX
		RET

L25$:
		CALL	GET_NEXT
		CALL	GET_NUMBER16
		CLC
		MOV	PACKDATA,EAX
		RET

L27$:
		CALL	GET_NEXT
		RESS	PACKDATA_FLAG
		SETT	NOPACKDATA_FLAG
		CLC
		RET

PROCESS_TLINK_P_SWITCH	ENDP


PROCESS_TLINK_T_SWITCH	PROC	NEAR
		;
		;Td OR Tw OR TLINK
		;OR Tp
		;
		MOV	AL,[ESI]
		INC	ESI
		DEC	ECX
		JZ	PTT_??
		CMP	AL,'d'
		JZ	PTS_Td
if	fg_pe
		CMP	AL,'p'
		JZ	PTS_Tp
endif
		CMP	AL,'w'
		JZ	PTS_Tw
		CMP	AL,'L'
		JNZ	PTT_??
		CLC
		RET

if	fg_pe

PTS_Tp:
		DEC	ECX
		MOV	EDX,8			;Tp = 8, Tpe = 9, Tpd = 10
		JZ	PTS_T_COMPLETE
		;
		;Tp, MAYBE Tpe OR Tpd
		;
		JMP	PTS_Twx

endif


PTS_Tw:
		DEC	ECX
		MOV	EDX,4			;Tw == 4
		JZ	PTS_T_COMPLETE
		;
		;Tw, MAYBE Twe OR Twd
		;
PTS_Twx:
		MOV	AL,[ESI]
		INC	ESI
		INC	EDX			;Twe == 5
		CMP	AL,'e'
		JZ	PTS_T_CX_COMPLETE
		INC	EDX			;Twd == 6
		CMP	AL,'d'
		JNZ	PTT_??
PTS_T_CX_COMPLETE:
		DEC	ECX
		JNZ	PTT_??
PTS_T_COMPLETE:
		CLC
		SETT	TLINK_OUTTYPE_FOUND
		MOV	TLINK_OUTTYPE,EDX
		RET

PTS_Td:
		;
		;Td, MAY BE Tdc OR Tde
		;
		XOR	EDX,EDX			;Td == 0
		DEC	ECX
		JZ	PTS_T_COMPLETE
		MOV	AL,[ESI]
		INC	ESI
		INC	EDX			;Tdc == 1
		CMP	AL,'c'
		JZ	PTS_T_CX_COMPLETE
		INC	EDX			;Tde == 2
		CMP	AL,'e'
		JZ	PTS_T_CX_COMPLETE
PTT_??:
		STC
		RET

PROCESS_TLINK_T_SWITCH	ENDP


PROCESS_TLINK_V_SWITCH	PROC
		;
		;
		;
		DEC	ECX
		JZ	PTV_??
		MOV	AL,[ESI]
		INC	ESI
		CMP	AL,'t'
		JNZ	PTV_??
		DEC	ECX
		JZ	L8$
		CALL	WARN_TLINK_XTRA
L8$:
		RET

PTV_??:
		STC
		RET

PROCESS_TLINK_V_SWITCH	ENDP


if	V5

PROCESS_TLINK_W_SWITCH	PROC	NEAR
		;
		;Wd, Wi, Wl, Wo, Wp, Wr, Ws, Wx
		;
		MOV	AL,[ESI]
		INC	ESI
		CMP	ECX,2
		JNZ	L99$
		CMP	AL,'d'
		JZ	T_Wd
		CMP	AL,'i'
		JZ	T_Wi
		CMP	AL,'l'
		JZ	T_Wl
		CMP	AL,'o'
		JZ	T_Wo
		CMP	AL,'p'
		JZ	T_Wp
		CMP	AL,'r'
		JZ	T_Wr
		CMP	AL,'s'
		JZ	T_Ws
		CMP	AL,'x'
		JZ	T_Wx
L99$:
		STC
		RET

T_Wd:
		;
		;/IMPDEF
		;
		CALL	SET_IMPDEF
		JMP	L9$

T_Wi:
		;
		;/INFORMATION
		;
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	T_WiM
		CMP	AL,'+'
		JNZ	T_WiX
		CALL	GET_NEXT
T_WiX:
		CALL	VERBOSE_PROC
		JMP	L9$

T_WiM:
		CALL	GET_NEXT
		CALL	SILENT_PROC
		JMP	L9$

T_Wl:
		;
		;/IMPLIB
		;
		CALL	SET_IMPLIB
		JMP	L9$			;CURRENTLY ALL I SUPPORT IS MANGLED NAMES...

T_Wo:
		CALL	SET_REORDER_ALLOWED
		JMP	L9$

T_Wp:
if	fg_winpack
		;
		;
		;
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	T_WpM
		CMP	AL,'+'
		JNZ	T_WpX
		CALL	GET_NEXT
T_WpX:
		CALL	SET_WINPACK
		JMP	L9$

T_WpM:
		CALL	GET_NEXT
		CALL	RES_WINPACK
endif
		JMP	L9$

T_Wr:
		;
		;RC HANDLER
		;
		CALL	RC_PROC
L9$:
		CLC
		RET

T_Ws:
		;
		;SET PAGESIZE
		;
		CALL	GET_NEXT
		CMP	AL,'='
		JZ	L7$
		CMP	AL,':'
		JNZ	L99$
L7$:
		CALL	GET_NUMBER16
		MOV	IMPLIB_PAGESIZE,EAX
		JMP	L9$

T_Wx:
		CALL	SEE_NEXT
		CMP	AL,'-'
		JZ	T_WxM
		CMP	AL,'+'
		JNZ	T_WxX
		CALL	GET_NEXT
T_WxX:
		CALL	MAKE_XREF
		JMP	L9$

T_WxM:
		CALL	GET_NEXT
		CALL	NO_MAKE_XREF
		JMP	L9$

PROCESS_TLINK_W_SWITCH	ENDP

endif	;V5


WARN_TLINK_XTRA	PROC	NEAR
		;
		;
		;
		MOV	AL,TLINK_XTRA_ERR
		CALL	WARN_SYMBOL_TEXT_RET
		RET

WARN_TLINK_XTRA	ENDP


SELECT_A_DEBUGTYPE	PROC	NEAR
		;
		;IF CODEVIEW IS NOT ENABLED, SELECT TURBODEBUGGER OUTPUT
		;
		BITT	CODEVIEW_FLAG
		JNZ	L9$
		SETT	TD_FLAG
L9$:
		RET

SELECT_A_DEBUGTYPE	ENDP

endif


SET_NONAMES	PROC
		;
		;
		;
		SETT	KILL_NONRESIDENT_NAMES
		RET

SET_NONAMES	ENDP


SET_BY_ORDINAL	PROC
		;
		;
		;
		SETT	ALL_EXPORTS_BY_ORDINAL
		RET

SET_BY_ORDINAL	ENDP


		.CONST

		ALIGN	4

EXE_TYPES	LABEL	DWORD

if	fg_pe
		DD	NT_STG,PE_EXE_TYPE
endif
		DD	WINDOWS_STG,WIN_SEGM_TYPE
		DD	OS2_STG,OS2_SEGM_TYPE
		DD	DOS_STG,DOS_EXE_TYPE
		DD	DOS4_STG,DOS4_SEGM_TYPE
		DD	DOSX_STG,DOSX_EXE_TYPE
		DD	UNKNOWN_STG,UNKNOWN_SEGM_TYPE

N_EXE_TYPES	EQU	($-EXE_TYPES)/8

SUBSYS_TYPES	LABEL	DWORD

		DD	PSUB_UNKNOWN_STG,PSUB_UNKNOWN
		DD	PSUB_NATIVE_STG,PSUB_NATIVE
		DD	PSUB_WIN_GUI_STG,PSUB_WIN_GUI
		DD	PSUB_WIN_CHAR_STG,PSUB_WIN_CHAR
		DD	PSUB_OS2_STG,PSUB_OS2
		DD	PSUB_POSIX_STG,PSUB_POSIX

N_SUBSYS_TYPES	EQU	($-SUBSYS_TYPES)/8


MS_CMD_PTRS	DD	LTR_A,LTR_B,LTR_C,LTR_D,LTR_E,LTR_F,LTR_G,LTR_H
		DD	LTR_I,LTR_J,LTR_K,LTR_L,LTR_M,LTR_N,LTR_O,LTR_P
		DD	LTR_Q,LTR_R,LTR_S,LTR_T,LTR_U,LTR_V,LTR_W,LTR_X
		DD	LTR_Y,LTR_Z,0


PSUB_UNKNOWN_STG	DB	'UNKNOWN',0
PSUB_NATIVE_STG		DB	'NATIVE',0
PSUB_WIN_GUI_STG	DB	'WINDOWS',0
PSUB_WIN_CHAR_STG	DB	'CONSOLE',0
PSUB_OS2_STG		DB	'OS2',0
PSUB_POSIX_STG		DB	'POSIX',0


LTR_A		LABEL	BYTE

		DB	9,1
		DCA	SET_ALIGNMENT
		DB	'ALIGNMENT'

if	any_overlays
		DB	8,3
		DCA	SET_ALLOCATE
		DB	'ALLOCATE'
endif

		DB	0


LTR_B		LABEL	BYTE

if	fg_pe
		DB	4,3
		DCA	SET_PE_BASE
		DB	'BASE'
endif

		DB	5,1
		DCA	BATCH_PROC
		DB	'BATCH'

		DB	6,2
		DCA	SET_BINARY
		DB	'BINARY'

if	V5
		DB	9,3
		DCA	SET_BY_ORDINAL
		DB	'BYORDINAL'
endif

		DB	0


LTR_C		LABEL	BYTE

if	any_overlays
		DB	5,2
		DCA	SET_CACHE
		DB	'CACHE'
endif

		DB	10,6
		DCA	SET_ABORT_CHK
		DB	'CHECKABORT'

		DB	8,6
		DCA	ENABLE_CHECKEXE
		DB	'CHECKEXE'

		DB	8,6
		DCA	CHECKSUM_PROC
		DB	'CHECKSUM'

if	fg_cv
		DB	8,2
		DCA	SET_CODEVIEW
		DB	'CODEVIEW'
endif

		DB	12,3
		DCA	SET_COMDEF_SEARCH
		DB	'COMDEFSEARCH'

		DB	12,2
		DCA	SET_CPARMAXALLOC
		DB	'CPARMAXALLOC'

if	fg_cv
		DB	9,3
		DCA	SET_CVVERSION
		DB	'CVVERSION'

		DB	10,3
		DCA	SET_CVWARNINGS
		DB	'CVWARNINGS'
endif

		DB	0


LTR_D		LABEL	BYTE

		DB	5,3
		DCA	SET_DEBUGALL
		DB	'DEBUG'

if	fg_winpack
		DB	14,6
		DCA	SET_DEBUGAPPLOADER
		DB	'DEBUGAPPLOADER'
endif

		DB	12,6
		DCA	SET_DEBUGBROWSERDEFS
		DB	'DEBUGBROWSER'

		DB	14,6
		DCA	SET_DEBUGCOVERAGES
		DB	'DEBUGCOVERAGES'

		DB	10,6
		DCA	HANDLE_DEBUGFILES
		DB	'DEBUGFILES'

		DB	10,7
		DCA	SET_DEBUGLINNUMS
		DB	'DEBUGLINES'

		DB	11,7
		DCA	SET_DEBUGLOCALS
		DB	'DEBUGLOCALS'

		DB	12,6
		DCA	HANDLE_DEBUGMODULES
		DB	'DEBUGMODULES'

		DB	12,6
		DCA	SET_DEBUGPUBLICS
		DB	'DEBUGPUBLICS'

		DB	15,6
		DCA	SET_DEBUGBROWSERREFS
		DB	'DEBUGREFERENCES'

		DB	10,6
		DCA	SET_DEBUGTYPES
		DB	'DEBUGTYPES'

		DB	20,3
		DCA	SET_DEFAULTLIBRARYSEARCH
		DB	'DEFAULTLIBRARYSEARCH'

		DB	13,2
		DCA	ERRORDELETE_PROC
		DB	'DELEXECUTABLE'

		DB	11,3
		DCA	SET_DETAILEDMAP
		DB	'DETAILEDMAP'

		DB	6,2
		DCA	DOSSEG_PROC
		DB	'DOSSEG'

if	fg_dospack

		DB	4,3
		DCA	SET_PACKDOTS
		DB	'DOTS'

endif

		DB	0


LTR_E		LABEL	BYTE

		DB	12,2
		DCA	SET_ECHO_IND
		DB	'ECHOINDIRECT'

		DB	10,4
		DCA	SET_EMSMAXSIZE
		DB	'EMSMAXSIZE'

		DB	14,4
		DCA	SET_EMS_IO_LEGAL
		DB	'EMSPAGEFRAMEIO'

		DB	8,4
		DCA	SET_EMSUSE40
		DB	'EMSUSE40'

		DB	5,2
		DCA	SET_ENTRYPOINT
		DB	'ENTRY'

if	fg_segm OR fg_pe
		DB	9,2
		DCA	ERRORFLAG_PROC
		DB	'ERRORFLAG'
endif

		DB	7,1
		DCA	SET_EXEPACK
		DB	'EXEPACK'

		DB	7,4
		DCA	EXETYPE_PROC
		DB	'EXETYPE'

		DB	0

LTR_F		LABEL	BYTE

		DB	18,1
		DCA	SET_FARCALLTRANSLATION
		DB	'FARCALLTRANSLATION'

if	fg_segm

		DB	5,2
		DCA	FIXDS_PROC
		DB	'FIXDS'

endif

if	fg_pe
		DB	5,4
		DCA	SET_PE_FIXED
		DB	'FIXED'
endif

IF 0
		DB	11,2
		DCA	SET_FORCEDEFINE
		DB	'FORCEDEFINE'
ENDIF
		DB	0

LTR_G		LABEL	BYTE

		DB	16,1
		DCA	SET_GROUPASSOCIATION
		DB	'GROUPASSOCIATION'

		DB	10,6
		DCA	GROUPSTACK_PROC
		DB	'GROUPSTACK'

		DB	0

LTR_H		LABEL	BYTE

if	fg_segm OR fg_pe
		DB	4,3
		DCA	SET_HEAP
		DB	'HEAP'
endif

		DB	4,1
		DCA	MS_HELP
		DB	'HELP'

;		DB	4,2
;		DCA	SET_HIGH
;		DB	'HIGH'

		DB	0

LTR_I		LABEL	BYTE

		DB	10,2
		DCA	IGNORECASE_PROC
		DB	'IGNORECASE'

;if	fg_segm
;		DB	11,3
;		DCA	SET_INCREMENTAL
;		DB	'INCREMENTAL'
;endif

if	V5
		DB	6,4
		DCA	SET_IMPDEF
		DB	'IMPDEF'

		DB	6,4
		DCA	SET_IMPLIB
		DB	'IMPLIB'

		DB	10,7
		DCA	SET_IMPLIB_COFF
		DB	'IMPLIBCOFF'
endif

		DB	11,1
		DCA	VERBOSE_PROC
		DB	'INFORMATION'

if	any_overlays
		DB	4,3
		DCA	INTO_PROC
		DB	'INTO'
endif

LTR_J		LABEL	BYTE
LTR_K		LABEL	BYTE

		DB	0

LTR_L		LABEL	BYTE

		DB	11,1
		DCA	SET_LINENUMBERS
		DB	'LINENUMBERS'

		DB	9,2
		DCA	LOWERCASE_PROC
		DB	'LOWERCASE'

		DB	0

LTR_M		LABEL	BYTE

if	fg_pe
		DB	7,3
		DCA	SET_MACHINE
		DB	'MACHINE'
endif

		DB	3,1
		DCA	MAKE_MAPOUT
		DB	'MAP'

		DB	0

LTR_N		LABEL	BYTE

if	any_overlays
		DB	4,2
		DCA	RES_MODEL_FAR
		DB	'NEAR'
endif

		DB	7,3
		DCA	RES_BATCH
		DB	'NOBATCH'

		DB	12,8
		DCA	RES_ABORT_CHK
		DB	'NOCHECKABORT'

		DB	10,8
		DCA	DISABLE_CHECKEXE
		DB	'NOCHECKEXE'

		DB	10,8
		DCA	NOCHECKSUM_PROC
		DB	'NOCHECKSUM'

		DB	10,4
		DCA	RES_CODEVIEW
		DB	'NOCODEVIEW'

		DB	14,5
		DCA	RES_COMDEF_SEARCH
		DB	'NOCOMDEFSEARCH'

		DB	8,4
		DCA	RES_CVPACK
		DB	'NOCVPACK'

		DB	7,5
		DCA	RES_DEBUGALL
		DB	'NODEBUG'

if	fg_winpack
		DB	16,8
		DCA	RES_DEBUGAPPLOADER
		DB	'NODEBUGAPPLOADER'
endif

		DB	14,8
		DCA	RES_DEBUGBROWSERDEFS
		DB	'NODEBUGBROWSER'

		DB	16,8
		DCA	RES_DEBUGCOVERAGES
		DB	'NODEBUGCOVERAGES'

		DB	12,9
		DCA	RES_DEBUGLINNUMS
		DB	'NODEBUGLINES'

		DB	13,9
		DCA	RES_DEBUGLOCALS
		DB	'NODEBUGLOCALS'

		DB	14,8
		DCA	RES_DEBUGPUBLICS
		DB	'NODEBUGPUBLICS'

		DB	17,8
		DCA	RES_DEBUGBROWSERREFS
		DB	'NODEBUGREFERENCES'

		DB	12,8
		DCA	RES_DEBUGTYPES
		DB	'NODEBUGTYPES'

		DB	22,3
		DCA	RES_DEFAULTLIBRARYSEARCH
		DB	'NODEFAULTLIBRARYSEARCH'

		DB	15,5
		DCA	NOERRORDELETE_PROC
		DB	'NODELEXECUTABLE'

		DB	13,5
		DCA	RES_DETAILEDMAP
		DB	'NODETAILEDMAP'

		DB	8,4
		DCA	SET_NODOSSEG
		DB	'NODOSSEG'

if	fg_dospack

		DB	6,5
		DCA	RES_PACKDOTS
		DB	'NODOTS'

endif

		DB	14,4
		DCA	RES_ECHO_IND
		DB	'NOECHOINDIRECT'

		DB	16,6
		DCA	RES_EMS_IO_LEGAL
		DB	'NOEMSPAGEFRAMEIO'

		DB	10,6
		DCA	RES_EMSUSE40
		DB	'NOEMSUSE40'

if	fg_segm OR fg_pe
		DB	11,4
		DCA	NOERRORFLAG_PROC
		DB	'NOERRORFLAG'
endif

		DB	9,5
		DCA	RES_EXEPACK
		DB	'NOEXEPACK'

		DB	15,3
		DCA	RES_EXTDICTIONARY
		DB	'NOEXTDICTIONARY'

		DB	20,3
		DCA	RES_FARCALLTRANSLATION
		DB	'NOFARCALLTRANSLATION'

if	fg_segm
		DB	7,4
		DCA	RES_FIXDS
		DB	'NOFIXDS'
endif

		DB	18,3
		DCA	RES_GROUPASSOCIATION
		DB	'NOGROUPASSOCIATION'

		DB	12,8
		DCA	NOGROUPSTACK_PROC
		DB	'NOGROUPSTACK'

		DB	12,3
		DCA	NOIGNORECASE_PROC
		DB	'NOIGNORECASE'

		DB	13,4
		DCA	RES_LINENUMBERS
		DB	'NOLINENUMBERS'

		DB	6,3
		DCA	RES_LOGO_OUTPUT
		DB	'NOLOGO'

		DB	5,3
		DCA	NO_MAKE_MAPOUT
		DB	'NOMAP'

if	V5
		DB	7,4
		DCA	SET_NONAMES
		DB	'NONAMES'
endif

		DB	8,4
		DCA	RES_NTHOST
		DB	'NONTHOST'

		DB	13,3
		DCA	NONULLSDOSSEG_PROC
		DB	'NONULLSDOSSEG'

if	any_overlays

		DB	14,3
		DCA	OVLCODELAST_PROC
		DB	'NOOVLCODEFIRST'

endif

		DB	10,3
		DCA	RES_PACKCODE
		DB	'NOPACKCODE'

		DB	10,7
		DCA	RES_PACKDATA
		DB	'NOPACKDATA'

		DB	15,7
		DCA	RES_PACKFUNCTIONS
		DB	'NOPACKFUNCTIONS'

		DB	18,7
		DCA	RES_PACKCODE_NO_SEGMENTS
		DB	'NOPACKIFNOSEGMENTS'

		DB	7,5
		DCA	RES_PAUSE
		DB	'NOPAUSE'

		DB	8,4
		DCA	RES_PROMPT_MODE
		DB	'NOPROMPT'

		DB	17,3
		DCA	RES_RELOCATION_CHECK
		DB	'NORELOCATIONCHECK'

if	any_overlays
		DB	8,5
		DCA	NORENAME_PROC
		DB	'NORENAME'
endif

		DB	17,5
		DCA	RES_REORDER_ALLOWED
		DB	'NOREORDERSEGMENTS'

		DB	9,9
		DCA	RES_SCANLIB
		DB	'NOSCANLIB'

		DB	10,10
		DCA	RES_SCANLINK
		DB	'NOSCANLINK'

if	fg_td
		DB	12,4
		DCA	RES_TD
		DB	'NOTURBODEBUG'
endif

		DB	10,7
		DCA	RES_WARNDUPS
		DB	'NOWARNDUPS'

if	fg_winpack
		DB	9,4
		DCA	RES_WINPACK
		DB	'NOWINPACK'
endif

		DB	6,3
		DCA	NO_MAKE_XREF
		DB	'NOXREF'

		DB	6,2
		DCA	SET_NTHOST
		DB	'NTHOST'

		DB	11,2
		DCA	SET_NULLSDOSSEG
		DB	'NULLSDOSSEG'

		DB	0

LTR_O		LABEL	BYTE

		DB	7,2
		DCA	ONERROR_PROC
		DB	'ONERROR'

		DB	3,3
		DCA	SET_OPTLINK
		DB	'OPT'

if	any_overlays
		DB	16,1
		DCA	SET_OVERLAYINTERRUPT
		DB	'OVERLAYINTERRUPT'

		DB	12,3
		DCA	OVLCODEFIRST_PROC
		DB	'OVLCODEFIRST'
endif

		DB	0

LTR_P		LABEL	BYTE

		DB	8,3
		DCA	SET_PACKCODE
		DB	'PACKCODE'

		DB	8,5
		DCA	SET_PACKDATA
		DB	'PACKDATA'

		DB	13,5
		DCA	SET_PACKFUNCTIONS
		DB	'PACKFUNCTIONS'

		DB	16,5
		DCA	SET_PACKCODE_NO_SEGMENTS
		DB	'PACKIFNOSEGMENTS'

		DB	8,5
		DCA	PACKSIZE_PROC
		DB	'PACKSIZE'

if	fg_segm OR fg_pe
		DB	7,4
		DCA	SET_PADCODE
		DB	'PADCODE'

		DB	7,4
		DCA	SET_PADDATA
		DB	'PADDATA'
endif

if	V5
		DB	8,3
		DCA	SET_PAGESIZE
		DB	'PAGESIZE'
endif

		DB	5,3
		DCA	SET_PAUSE
		DB	'PAUSE'

if	fg_segm
		DB	6,2
		DCA	PMTYPE_PROC
		DB	'PMTYPE'
endif

		DB	6,2
		DCA	SET_PROMPT_MODE
		DB	'PROMPT'

		DB	0

LTR_Q		LABEL	BYTE

if	fg_dospack
		DB	9,1
		DCA	SET_QUIKPACK
		DB	'QUICKPACK'
endif

		DB	0

LTR_R		LABEL	BYTE

if	fg_segm OR fg_pe
		DB	2,2
		DCA	RC_PROC
		DB	'RC'
endif

if	any_overlays
		DB	6,5
		DCA	SET_RELOAD
		DB	'RELOAD'
endif

		DB	15,5
		DCA	SET_RELOCATION_CHECK
		DB	'RELOCATIONCHECK'

		DB	15,3
		DCA	SET_REORDER_ALLOWED
		DB	'REORDERSEGMENTS'

		DB	0

LTR_S		LABEL	BYTE

		DB	7,7
		DCA	SET_SCANLIB
		DB	'SCANLIB'

		DB	8,8
		DCA	SET_SCANLINK
		DB	'SCANLINK'

		DB	8,2
		DCA	SET_SEGMENTS
		DB	'SEGMENTS'

if	fg_winpack
		DB	7,4
		DCA	SET_SEGPACK
		DB	'SEGPACK'
endif

		DB	6,2
		DCA	SILENT_PROC
		DB	'SILENT'

if	fg_dospack
		DB	7,2
		DCA	SET_SLRPACK
		DB	'SLRPACK'
endif

;		DB	10,2
;		DCA	SET_SOFTEXTRNS
;		DB	'SOFTEXTRNS'

		DB	5,2
		DCA	SET_STACK
		DB	'STACK'

if	fg_segm OR fg_pe
		DB	4,3
		DCA	STUB_PROC
		DB	'STUB'
endif

if	fg_pe
		DB	9,2
		DCA	SUBSYSTEM_PROC
		DB	'SUBSYSTEM'
endif

		DB	0

LTR_T		LABEL	BYTE

if	fg_td
		DB	9,3
		DCA	SET_TDVERSION
		DB	'TDVERSION'
endif

		DB	4,1
		DCA	SET_TINY
		DB	'TINY'

if	fg_td
		DB	5,2
		DCA	SET_TLINK
		DB	'TLINK'

		DB	10,2
		DCA	SET_TD
		DB	'TURBODEBUG'
endif

		DB	0

LTR_U		LABEL	BYTE

		DB	9,1
		DCA	UPPERCASE_PROC
		DB	'UPPERCASE'

		DB	0


LTR_V		LABEL	BYTE

if	fg_pe
		DB	7,4
		DCA	SET_VERSION
		DB	'VERSION'
endif

		DB	0

LTR_W		LABEL	BYTE

if	fg_segm
		DB	9,1
		DCA	SET_WARNFIXUP
		DB	'WARNFIXUP'
endif

		DB	8,5
		DCA	SET_WARNDUPS
		DB	'WARNDUPS'

if	fg_winpack
		DB	7,2
		DCA	SET_WINPACK
		DB	'WINPACK'
endif

		DB	0

LTR_X		LABEL	BYTE

		DB	10,2
		DCA	SET_XMSMAXSIZE
		DB	'XMSMAXSIZE'

		DB	4,1
		DCA	MAKE_XREF
		DB	'XREF'

if	fg_segm OR fg_pe

		DB	13,2
		DCA	XNOIGNORECASE_PROC
		DB	'XNOIGNORECASE'

		DB	10,2
		DCA	XUPPER_PROC
		DB	'XUPPERCASE'

endif

LTR_Y		LABEL	BYTE
LTR_Z		LABEL	BYTE

		DB	0

if	fg_segm

PM_TYPES	DD	PM_STG,300H
		DD	VIO_STG,200H
		DD	NOVIO_STG,100H

PM_STG		DB	'PM',0
VIO_STG		DB	'VIO',0
NOVIO_STG	DB	'NOVIO',0

endif

if	fg_pe
NT_STG		DB	'NT',0
endif
OS2_STG		DB	'OS2',0
WINDOWS_STG	DB	'WINDOWS',0
DOS_STG		DB	'DOS',0
DOS4_STG	DB	'DOS4',0
DOSX_STG	DB	'DOSX',0
UNKNOWN_STG	DB	'UNKNOWN',0

FULL_TEXT	DB	'FULL',0
ADDRESS_TEXT	DB	'ADDRESS',0
GROUP_TEXT	DB	'GROUP',0
NOGROUP_TEXT	DB	'NOGROUP',0

NOEXE_TXT	DB	'NOEXE',0

if	fg_pe

I386_TXT	DB	'I386',0

endif

		END

